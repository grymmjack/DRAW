''
' DRAW - OUTPUT/SCREEN.BM
' =============================================================================
' Screen output files subs and functions.
'
' @todo
' @depends ./SCREEN.BI
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Helper for console output in graphics mode
'
SUB ECHO_LOG (msg AS STRING)
    DIM d AS LONG
    d& = _DEST
    _DEST _CONSOLE
    PRINT msg
    _DEST d&
END SUB

''
' Detects the resolution of the desktop and sets the width and height to It
' 
SUB SCREEN_detect_desktop_resolution()
    DO: _LIMIT CFG.FPS_LIMIT% : LOOP UNTIL _SCREENEXISTS
    SCRN.w& = _DESKTOPWIDTH : SCRN.h& = _DESKTOPHEIGHT
END SUB


''
' Initialize the screen and various images we will use
' 
SUB SCREEN_init ()
    SCRN.w&   = 0
    SCRN.h&   = 0
    SCRN.bpp% = 0
    SCREEN_detect_desktop_resolution
    'override for 320x200
    SCRN.w& = 320
    SCRN.h& = 200 
    SCRN.zoom! = 1.0      ' Start at 1x zoom
    SCRN.zoomMin! = 0.25  ' 25% minimum zoom
    SCRN.zoomMax! = 8.0   ' 8x maximum zoom
    SCRN.offsetX% = 0
    SCRN.offsetY% = 0
    SCRN.panning% = FALSE
    SCRN.panStartX% = 0
    SCRN.panStartY% = 0
    SCRN.panOrigOffsetX% = 0
    SCRN.panOrigOffsetY% = 0
    SCRN.showToolbar% = TRUE
    SCRN.showStatus% = TRUE
    SCRN.toolbarManuallyHidden% = FALSE
    SCRN.statusManuallyHidden% = FALSE
    SCRN.CANVAS&   = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    SCRN.PAINTING& = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    SCRN.GUI&      = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    SCRN.CURSOR&   = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    _DEST SCRN.PAINTING&
    CLS , _RGBA32(0, 0, 0, 0)
    _DEST SCRN.GUI&
    SCREEN SCRN.CANVAS&
    _DEST SCRN.CANVAS&
    IF CFG.FULLSCREEN% THEN _FULLSCREEN _SQUAREPIXELS
    SCRN.bpp% = _PIXELSIZE
    ' Initialize tools
    MOVE_init
    TEXT_init
    GRID_init
END SUB


''
' Draws screen
' 
SUB SCREEN_render ()
    ' todo get status to hide when painting 
    _DEST SCRN.CANVAS&
    CLS
    
    ' Clear GUI layer and render toolbar/status conditionally
    _DEST SCRN.GUI&
    CLS , _RGBA32(0, 0, 0, 0) ' Clear with transparency
    
    IF SCRN.showToolbar% THEN
        TOOLBAR_render 0, 0, 13  ' xPos parameter not used anymore
    END IF
    IF SCRN.showStatus% THEN STATUS_render
    
    ' Switch back to canvas for main rendering
    _DEST SCRN.CANVAS&
    
    ' Calculate zoomed dimensions and position
    DIM zw AS LONG, zh AS LONG, dx AS INTEGER, dy AS INTEGER
    zw& = SCRN.w& * SCRN.zoom!
    zh& = SCRN.h& * SCRN.zoom!
    dx% = (_WIDTH - zw&) \ 2 + SCRN.offsetX%
    dy% = (_HEIGHT - zh&) \ 2 + SCRN.offsetY%
    
    ' Render painting layer with zoom
    _DEST SCRN.CANVAS& : _SOURCE SCRN.PAINTING&
    _PUTIMAGE (dx%, dy%)-(dx% + zw& - 1, dy% + zh& - 1), SCRN.PAINTING&
    
    ' Draw grid overlay if enabled (after painting, before border/GUI)
    IF GRID.SHOW% THEN
        _DEST SCRN.CANVAS&
        _SOURCE GRID.imgHandle&
        _BLEND GRID.imgHandle&
        _PUTIMAGE (dx%, dy%)-(dx% + zw& - 1, dy% + zh& - 1), GRID.imgHandle&, SCRN.CANVAS&
    END IF
    
    ' Draw border around canvas (after painting layer)
    LINE (dx% - 1, dy% - 1)-(dx% + zw&, dy% + zh&), _RGB32(51, 51, 51), B
    
    _DEST SCRN.CANVAS& : _SOURCE SCRN.GUI& : _PUTIMAGE
    ' Draw MARQUEE selection if active or dragging (after all layers so it's on top)
    IF CURRENT_TOOL% = TOOL_MARQUEE AND (MARQUEE.ACTIVE% OR MARQUEE.DRAGGING%) THEN
        _DEST SCRN.CANVAS&
        MARQUEE_draw TRUE
    END IF
    ' Draw MOVE transform preview if active
    IF CURRENT_TOOL% = TOOL_MOVE AND MOVE.ACTIVE THEN
        _DEST SCRN.CANVAS&
        MOVE_render_preview SCRN.CANVAS&
        ' Also draw marquee handles around the transformed selection
        MARQUEE_draw TRUE
    END IF
    ' Draw TEXT preview if editing
    IF CURRENT_TOOL% = TOOL_TEXT AND TEXT.ACTIVE THEN
        _DEST SCRN.CANVAS&
        TEXT_render_preview SCRN.CANVAS&
    END IF
    ' Draw LINE preview while dragging
    IF CURRENT_TOOL% = TOOL_LINE AND LINE_TOOL.DRAGGING THEN
        _DEST SCRN.CANVAS&
        LINE (LINE_TOOL.START_X, LINE_TOOL.START_Y)-(LINE_TOOL.END_X, LINE_TOOL.END_Y), PAINT_COLOR~&
    END IF
    ' Draw POLY_LINE preview from last point to mouse cursor
    IF (CURRENT_TOOL% = TOOL_POLYGON OR CURRENT_TOOL% = TOOL_POLYGON_FILLED) AND POLY_LINE.HAS_LAST THEN
        _DEST SCRN.CANVAS&
        LINE (POLY_LINE.LAST_X, POLY_LINE.LAST_Y)-(MOUSE.X%, MOUSE.Y%), PAINT_COLOR~&
    END IF
    ' Draw RECT preview while dragging
    IF (CURRENT_TOOL% = TOOL_RECT OR CURRENT_TOOL% = TOOL_RECT_FILLED) AND RECT_TOOL.DRAGGING THEN
        _DEST SCRN.CANVAS&
        IF RECT_TOOL.FILLED THEN
            LINE (RECT_TOOL.START_X, RECT_TOOL.START_Y)-(RECT_TOOL.END_X, RECT_TOOL.END_Y), PAINT_COLOR~&, BF
        ELSE
            LINE (RECT_TOOL.START_X, RECT_TOOL.START_Y)-(RECT_TOOL.END_X, RECT_TOOL.END_Y), PAINT_COLOR~&, B
        END IF
    END IF
    ' Draw ELLIPSE preview while dragging
    IF (CURRENT_TOOL% = TOOL_ELLIPSE OR CURRENT_TOOL% = TOOL_ELLIPSE_FILLED) AND ELLIPSE_TOOL.DRAGGING THEN
        DIM pcx AS INTEGER, pcy AS INTEGER, prx AS SINGLE, pry AS SINGLE, pasp AS SINGLE
        pcx% = (ELLIPSE_TOOL.START_X + ELLIPSE_TOOL.END_X) \ 2
        pcy% = (ELLIPSE_TOOL.START_Y + ELLIPSE_TOOL.END_Y) \ 2
        prx! = ABS(ELLIPSE_TOOL.END_X - ELLIPSE_TOOL.START_X) / 2
        pry! = ABS(ELLIPSE_TOOL.END_Y - ELLIPSE_TOOL.START_Y) / 2
        IF prx! > 0 THEN pasp! = pry! / prx! ELSE pasp! = 1
        _DEST SCRN.CANVAS&
        IF prx! > 0 THEN
            IF ELLIPSE_TOOL.FILLED THEN
                ' Draw filled ellipse using CIRCLE outline + PAINT
                CIRCLE (pcx%, pcy%), prx!, PAINT_COLOR~&, , , pasp!
                PAINT (pcx%, pcy%), PAINT_COLOR~&, PAINT_COLOR~&
            ELSE
                ' Draw ellipse outline
                CIRCLE (pcx%, pcy%), prx!, PAINT_COLOR~&, , , pasp!
            END IF
        END IF
    END IF
    ' Render crosshair AFTER GUI layer so it's visible (using SHIFT key)
    IF _KEYDOWN(KEY_LSHIFT&) OR _KEYDOWN(KEY_RSHIFT&) THEN 
        CROSSHAIR_render PAL_color(THEME.CROSSHAIR_fg%)
    END IF
    POINTER_update
    POINTER_render
    _DEST SCRN.CANVAS& : _SOURCE SCRN.CURSOR&
    _CLEARCOLOR _RGB32(0, 0, 0)
    _SETALPHA 0, _RGB32(0,0,0), SCRN.CURSOR&
    _BLEND SCRN.CURSOR&
    _PUTIMAGE
    _DISPLAY
END SUB
