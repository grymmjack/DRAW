''
' DRAW - OUTPUT/SCREEN.BM
' =============================================================================
' Screen output files subs and functions.
'
' @todo
' @depends ./SCREEN.BI
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Detects the resolution of the desktop and sets the width and height to It
' 
SUB SCREEN_detect_desktop_resolution()
    DO: _LIMIT CFG.FPS_LIMIT% : LOOP UNTIL _SCREENEXISTS
    SCRN.w& = _DESKTOPWIDTH : SCRN.h& = _DESKTOPHEIGHT
END SUB


''
' Initialize the screen and various images we will use
' 
SUB SCREEN_init ()
    SCRN.w&   = 0
    SCRN.h&   = 0
    SCRN.bpp% = 0
    SCREEN_detect_desktop_resolution
    'override for 320x200
    SCRN.w& = 320
    SCRN.h& = 200 
    SCRN.zoom! = 1.0      ' Start at 1x zoom
    SCRN.zoomMin! = 0.25  ' 25% minimum zoom
    SCRN.zoomMax! = 8.0   ' 8x maximum zoom
    SCRN.offsetX% = 0
    SCRN.offsetY% = 0
    SCRN.panning% = FALSE
    SCRN.panStartX% = 0
    SCRN.panStartY% = 0
    SCRN.panOrigOffsetX% = 0
    SCRN.panOrigOffsetY% = 0
    SCRN.CANVAS&   = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    SCRN.PAINTING& = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    SCRN.GUI&      = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    SCRN.CURSOR&   = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    _DEST SCRN.PAINTING&
    CLS , _RGBA32(0, 0, 0, 0)
    _DEST SCRN.GUI&
    SCREEN SCRN.CANVAS&
    _DEST SCRN.CANVAS&
    IF CFG.FULLSCREEN% THEN _FULLSCREEN _SQUAREPIXELS
    SCRN.bpp% = _PIXELSIZE    
END SUB


''
' Draws screen
' 
SUB SCREEN_render ()
    ' todo get status to hide when painting 
    CLS
    TOOLBAR_render TB_RIGHT, 0, 6
    TOOLBAR_render TB_LEFT, 7, 13
    STATUS_render
    
    ' Calculate zoomed dimensions and position
    DIM zw AS LONG, zh AS LONG, dx AS INTEGER, dy AS INTEGER
    zw& = SCRN.w& * SCRN.zoom!
    zh& = SCRN.h& * SCRN.zoom!
    dx% = (_WIDTH - zw&) \ 2 + SCRN.offsetX%
    dy% = (_HEIGHT - zh&) \ 2 + SCRN.offsetY%
    
    ' Render painting layer with zoom
    _DEST SCRN.CANVAS& : _SOURCE SCRN.PAINTING&
    _PUTIMAGE (dx%, dy%)-(dx% + zw& - 1, dy% + zh& - 1), SCRN.PAINTING&
    
    ' Draw border around canvas (after painting layer)
    LINE (dx% - 1, dy% - 1)-(dx% + zw&, dy% + zh&), _RGB32(51, 51, 51), B
    
    IF _KEYDOWN(KEY_CAPSLOCK&) THEN CROSSHAIR_render PAL_color(THEME.CROSSHAIR_fg%)
    _DEST SCRN.CANVAS& : _SOURCE SCRN.GUI& : _PUTIMAGE
    ' Draw MARQUEE selection if active or dragging (after all layers so it's on top)
    IF CURRENT_TOOL% = TOOL_MARQUEE AND (MARQUEE.ACTIVE% OR MARQUEE.DRAGGING%) THEN
        _DEST SCRN.CANVAS&
        MARQUEE_draw TRUE
    END IF
    POINTER_update
    POINTER_render
    _DEST SCRN.CANVAS& : _SOURCE SCRN.CURSOR&
    _CLEARCOLOR _RGB32(0, 0, 0)
    _SETALPHA 0, _RGB32(0,0,0), SCRN.CURSOR&
    _BLEND SCRN.CURSOR&
    _PUTIMAGE
    _DISPLAY
END SUB
