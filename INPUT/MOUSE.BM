''
' DRAW - INPUT/MOUSE.BM
' =============================================================================
' Mouse input handling subs and functions.
'
' @todo
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Initializes the mouse
' 
SUB MOUSE_init ()
    MOUSE.X%     = 1
    MOUSE.Y%     = 1
    MOUSE.OLD_X% = 1
    MOUSE.OLD_Y% = 1
    MOUSE.CON_X% = 0
    MOUSE.CON_Y% = 0
    MOUSE.OSW%   = 0
    MOUSE.B1%    = FALSE
    MOUSE.B2%    = FALSE
    MOUSE.B3%    = FALSE
    MOUSE.OLD_B1% = FALSE
    MOUSE.DRAG$  = ""
END SUB


''
' Handles mouse input
' 
SUB MOUSE_input_handler ()
    DIM saved_state_this_frame AS INTEGER
    saved_state_this_frame% = FALSE
    
    WHILE _MOUSEINPUT:
        MOUSE.X%  = _MOUSEX
        MOUSE.Y%  = _MOUSEY
        MOUSE.B1% = _MOUSEBUTTON(1)
        MOUSE.B2% = _MOUSEBUTTON(2)
        MOUSE.B3% = _MOUSEBUTTON(3)
        IF MOUSE.B1% THEN
            ' Handle different tools based on CURRENT_TOOL
            SELECT CASE CURRENT_TOOL%
                CASE TOOL_FILL
                    ' Flood fill on click (only on button press, not while held)
                    IF FILL.ACTIVE% AND NOT MOUSE.OLD_B1% AND NOT saved_state_this_frame% THEN
                        saved_state_this_frame% = TRUE
                        FILL.COLOR~& = PAINT_COLOR~&
                        FILL_flood MOUSE.X%, MOUSE.Y%
                        UNDO_save_state ' Save state after fill
                    END IF
                CASE TOOL_BRUSH, TOOL_DOT, TOOL_LINE
                    ' Always draw while button is held
                    IF MOUSE.X% < MOUSE.OLD_X% THEN MOUSE.DRAG$ = "L"
                    IF MOUSE.X% > MOUSE.OLD_X% THEN MOUSE.DRAG$ = "R"
                    IF MOUSE.Y% < MOUSE.OLD_Y% THEN MOUSE.DRAG$ = "U"
                    IF MOUSE.Y% > MOUSE.OLD_Y% THEN MOUSE.DRAG$ = "D"
                    PAINT_on
            END SELECT
        ELSE
            ' Button released - save undo state if we were drawing
            IF MOUSE.OLD_B1% AND NOT saved_state_this_frame% THEN
                SELECT CASE CURRENT_TOOL%
                    CASE TOOL_BRUSH, TOOL_DOT, TOOL_LINE
                        UNDO_save_state
                        saved_state_this_frame% = TRUE
                END SELECT
            END IF
            MOUSE.DRAG$ = ""
            MOUSE.CON_X% = 0 : MOUSE.CON_Y% = 0
            CONSTRAIN_X% = FALSE : CONSTRAIN_Y% = FALSE
        END IF
        IF MOUSE.SW% <> 0 THEN
            ' Check if CTRL is held for brush size adjustment
            IF (_KEYDOWN(100305) OR _KEYDOWN(100306)) THEN ' Right or Left CTRL
                ' CTRL+Wheel adjusts brush size
                IF MOUSE.SW% > 0 THEN
                    BRUSH_SIZE_increase
                ELSE
                    BRUSH_SIZE_decrease
                END IF
            ELSE
                ' No CTRL, adjust color as normal
                PAL_FG_IDX% = PAL_clamp_color_index(PAL_FG_IDX% + -MOUSE.SW%)
                PAINT_COLOR~& = PAL_color(PAL_FG_IDX%)
                IF NOT KEY_COLOR% THEN
                    WHEEL_COLOR% = TRUE
                    KEY_COLOR%   = FALSE
                END IF
            END IF
            MOUSE.OSW% = MOUSE.SW%
        END IF
        MOUSE.SW% = _MOUSEWHEEL

        ' DO: LOOP UNTIL _MOUSEINPUT = 0

        IF _KEYDOWN(KEY_CAPSLOCK&) AND NOT CONSTRAIN_X% AND NOT CONSTRAIN_Y% THEN
            IF MOUSE.DRAG$ = "L" OR MOUSE.DRAG$ = "R" THEN
                MOUSE.CON_Y% = MOUSE.Y%
                CONSTRAIN_X% = FALSE
                CONSTRAIN_Y% = TRUE
            ELSEIF MOUSE.DRAG$ = "U" OR MOUSE.DRAG$ = "D" THEN
                MOUSE.CON_X% = MOUSE.X%
                CONSTRAIN_X% = TRUE
                CONSTRAIN_Y% = FALSE
            END IF
        END IF
    WEND
END SUB


''
' Finishing stuff in the mouse input loop
' 
SUB MOUSE_input_handler_loop ()
    MOUSE.OLD_X% = MOUSE.X% : MOUSE.OLD_Y% = MOUSE.Y%
    MOUSE.OLD_B1% = MOUSE.B1%
END SUB
