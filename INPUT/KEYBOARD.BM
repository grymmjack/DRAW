''
' DRAW - INPUT/KEYBOARD.BM
' =============================================================================
' Keyboard input handling subs and functions.
'
' @todo
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Handles changing colors with keyboard
' @todo
' 
SUB KEYBOARD_colors (keypress AS STRING)
    SELECT CASE keypress$
        CASE "0": PAINT_COLOR~& = PAL_color(0) : PAL_FG_IDX% = 0
        CASE "1": PAINT_COLOR~& = PAL_color(1) : PAL_FG_IDX% = 1
        CASE "2": PAINT_COLOR~& = PAL_color(2) : PAL_FG_IDX% = 2
        CASE "3": PAINT_COLOR~& = PAL_color(3) : PAL_FG_IDX% = 3
        CASE "4": PAINT_COLOR~& = PAL_color(4) : PAL_FG_IDX% = 4
        CASE "5": PAINT_COLOR~& = PAL_color(5) : PAL_FG_IDX% = 5
        CASE "6": PAINT_COLOR~& = PAL_color(6) : PAL_FG_IDX% = 6
        CASE "7": PAINT_COLOR~& = PAL_color(7) : PAL_FG_IDX% = 7
        CASE ")": PAINT_COLOR~& = PAL_color(8) : PAL_FG_IDX% = 8
        CASE "8": PAINT_COLOR~& = PAL_color(8) : PAL_FG_IDX% = 8
        CASE "!": PAINT_COLOR~& = PAL_color(9) : PAL_FG_IDX% = 9 
        CASE "9": PAINT_COLOR~& = PAL_color(9) : PAL_FG_IDX% = 9 
        CASE "@": PAINT_COLOR~& = PAL_color(10) : PAL_FG_IDX% = 10
        CASE "#": PAINT_COLOR~& = PAL_color(11) : PAL_FG_IDX% = 11
        CASE "$": PAINT_COLOR~& = PAL_color(12) : PAL_FG_IDX% = 12
        CASE "%": PAINT_COLOR~& = PAL_color(13) : PAL_FG_IDX% = 13
        CASE "^": PAINT_COLOR~& = PAL_color(14) : PAL_FG_IDX% = 14
        CASE "&": PAINT_COLOR~& = PAL_color(15) : PAL_FG_IDX% = 15
        EVERYCASE:
            KEY_COLOR%   = TRUE 
            WHEEL_COLOR% = FALSE
    END SELECT
END SUB


''
' Handles tool selection with keyboard
'
SUB KEYBOARD_tools (keypress AS STRING)
    SELECT CASE keypress$
        CASE "b", "B"
            CURRENT_TOOL% = TOOL_BRUSH
            FILL_deactivate
        CASE "f", "F"
            CURRENT_TOOL% = TOOL_FILL
            FILL_activate
        CASE "d", "D"
            CURRENT_TOOL% = TOOL_DOT
            FILL_deactivate
        CASE "l", "L"
            CURRENT_TOOL% = TOOL_LINE
            FILL_deactivate
        CASE "m", "M"
            CURRENT_TOOL% = TOOL_MARQUEE
            FILL_deactivate
        CASE CHR$(26), CHR$(90), CHR$(122) ' Ctrl+Z (works with both z and Z)
            UNDO_undo
        CASE CHR$(25), CHR$(89), CHR$(121) ' Ctrl+Y (works with both y and Y)
            UNDO_redo
    END SELECT
END SUB


''
' Handles brush size changes with keyboard
'
SUB KEYBOARD_brush_size (keypress AS STRING)
    SELECT CASE keypress$
        CASE "[", "{"
            BRUSH_SIZE_decrease
        CASE "]", "}"
            BRUSH_SIZE_increase
        CASE CHR$(0) + CHR$(59)  ' F1
            BRUSH_SIZE_set_preset 1
        CASE CHR$(0) + CHR$(60)  ' F2
            BRUSH_SIZE_set_preset 2
        CASE CHR$(0) + CHR$(61)  ' F3
            BRUSH_SIZE_set_preset 3
        CASE CHR$(0) + CHR$(62)  ' F4
            BRUSH_SIZE_set_preset 4
    END SELECT
END SUB


''
' Handles assistant toggles with keyboard
'
SUB KEYBOARD_assistants (keypress AS STRING)
    SELECT CASE keypress$
        CASE "~", "`"
            BRUSH_SIZE_toggle_preview
        CASE "|", "\"
            BRUSH_SIZE_toggle_shape
    END SELECT
END SUB


''
' Handle keyboard input
' 
SUB KEYBOARD_input_handler ()
    DIM keypress AS STRING
    keypress$ = INKEY$
    IF keypress$ <> "" THEN
        KEYBOARD_colors keypress$
        KEYBOARD_tools keypress$
        KEYBOARD_brush_size keypress$
        KEYBOARD_assistants keypress$
    END IF
    
    ' Marquee tool keyboard controls
    IF CURRENT_TOOL% = TOOL_MARQUEE AND MARQUEE.ACTIVE% THEN
        DIM isCtrl AS INTEGER, isShift AS INTEGER, moveStep AS INTEGER
        isCtrl% = (_KEYDOWN(100305) OR _KEYDOWN(100306)) ' Left or Right Ctrl
        isShift% = (_KEYDOWN(100304) OR _KEYDOWN(100303)) ' Left or Right Shift
        moveStep% = 1
        IF isShift% THEN moveStep% = 10
        
        ' Arrow keys move or resize the marquee
        IF _KEYDOWN(18432) THEN ' Up arrow
            IF isCtrl% THEN
                MARQUEE.BOX.h = MARQUEE.BOX.h - moveStep%
                IF MARQUEE.BOX.h < 8 THEN MARQUEE.BOX.h = 8
            ELSE
                MARQUEE.BOX.y = MARQUEE.BOX.y - moveStep%
            END IF
        END IF
        IF _KEYDOWN(20480) THEN ' Down arrow
            IF isCtrl% THEN
                MARQUEE.BOX.h = MARQUEE.BOX.h + moveStep%
            ELSE
                MARQUEE.BOX.y = MARQUEE.BOX.y + moveStep%
            END IF
        END IF
        IF _KEYDOWN(19200) THEN ' Left arrow
            IF isCtrl% THEN
                MARQUEE.BOX.w = MARQUEE.BOX.w - moveStep%
                IF MARQUEE.BOX.w < 8 THEN MARQUEE.BOX.w = 8
            ELSE
                MARQUEE.BOX.x = MARQUEE.BOX.x - moveStep%
            END IF
        END IF
        IF _KEYDOWN(19712) THEN ' Right arrow
            IF isCtrl% THEN
                MARQUEE.BOX.w = MARQUEE.BOX.w + moveStep%
            ELSE
                MARQUEE.BOX.x = MARQUEE.BOX.x + moveStep%
            END IF
        END IF
    END IF
    
    ' Check for Ctrl-D to deselect marquee
    IF _KEYDOWN(100305) OR _KEYDOWN(100306) THEN ' Left or Right Ctrl
        IF _KEYDOWN(100) THEN ' D key
            IF CURRENT_TOOL% = TOOL_MARQUEE THEN
                MARQUEE_clear
            END IF
        END IF
        
        ' Zoom shortcuts
        IF _KEYDOWN(48) THEN ' CTRL+0 - reset zoom to 100% and center (48 = "0" key)
            SCRN.zoom! = 1.0
            SCRN.offsetX% = 0
            SCRN.offsetY% = 0
        END IF
        IF _KEYDOWN(61) THEN ' CTRL+= - zoom in 25% (61 = "=" key)
            SCRN.zoom! = SCRN.zoom! + 0.25
            IF SCRN.zoom! > SCRN.zoomMax! THEN SCRN.zoom! = SCRN.zoomMax!
        END IF
        IF _KEYDOWN(45) THEN ' CTRL+- - zoom out 25% (45 = "-" key)
            SCRN.zoom! = SCRN.zoom! - 0.25
            IF SCRN.zoom! < SCRN.zoomMin! THEN SCRN.zoom! = SCRN.zoomMin!
        END IF
        IF _KEYDOWN(50) THEN ' CTRL+2 - 200% zoom (50 = "2" key)
            SCRN.zoom! = 2.0
        END IF
        IF _KEYDOWN(46) THEN ' CTRL+. - 50% zoom (46 = "." key)
            SCRN.zoom! = 0.5
        END IF
    END IF
END SUB


''
' Finishing stuff in the keyboard input loop
' 
SUB KEYBOARD_input_handler_loop ()
END SUB