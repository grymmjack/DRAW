''
' DRAW - GUI/COMMAND.BM
' =============================================================================
' Command palette implementation - searchable command list with fuzzy matching.
'
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Initialize command palette and register all commands
'
SUB CMD_init ()
    CMD_COUNT = 0
    CMD_PALETTE.visible = FALSE
    CMD_PALETTE.search = ""
    CMD_PALETTE.search_len = 0
    CMD_PALETTE.hover = 0
    CMD_PALETTE.scroll = 0
    CMD_PALETTE.result_count = 0
    CMD_PALETTE.mode = 0
    
    ' Load Tiny5 font with no blending for pixelated look (same size as status bar)
    CMD_FONT_HANDLE = _LOADFONT("ASSETS/FONTS/Tiny5-Regular.ttf", 8, "DONTBLEND")
    
    ' Calculate palette dimensions (using Tiny5 font metrics)
    ' Width scales with canvas (55%, clamped 280..500) so it fits small screens
    CMD_PALETTE_W = INT(SCRN.w& * 0.55)
    IF CMD_PALETTE_W < 280 THEN CMD_PALETTE_W = 280
    IF CMD_PALETTE_W > 500 THEN CMD_PALETTE_W = 500
    CMD_PALETTE_H = (CMD_MAX_VISIBLE + 2) * 14 + 30  ' header + search + items
    CMD_PALETTE_X = (SCRN.w& - CMD_PALETTE_W) \ 2
    CMD_PALETTE_Y = 40
    
    ' Register all commands
    ' ========== TOOLS ==========
    CMD_register "Brush Tool", "B", CMD_CAT_TOOL, 101
    CMD_register "Dot Tool", "D", CMD_CAT_TOOL, 102
    CMD_register "Fill Tool", "F", CMD_CAT_TOOL, 103
    CMD_register "Color Picker", "I", CMD_CAT_TOOL, 104
    CMD_register "Line Tool", "L", CMD_CAT_TOOL, 105
    CMD_register "Polygon Tool", "P", CMD_CAT_TOOL, 106
    CMD_register "Polygon Filled", "Shift+P", CMD_CAT_TOOL, 107
    CMD_register "Rectangle Tool", "R", CMD_CAT_TOOL, 108
    CMD_register "Rectangle Filled", "Shift+R", CMD_CAT_TOOL, 109
    CMD_register "Ellipse Tool", "E", CMD_CAT_TOOL, 110
    CMD_register "Ellipse Filled", "Shift+E", CMD_CAT_TOOL, 111
    CMD_register "Marquee Tool", "M", CMD_CAT_TOOL, 112
    CMD_register "Magic Wand", "W", CMD_CAT_TOOL, 117
    CMD_register "Move Tool", "V", CMD_CAT_TOOL, 113
    CMD_register "Text Tool", "T", CMD_CAT_TOOL, 114
    CMD_register "Text Tool (Tiny5)", "Shift+T", CMD_CAT_TOOL, 115
    CMD_register "Text Tool (Custom Font)", "Ctrl+T", CMD_CAT_TOOL, 116
    
    ' ========== FILE ==========
    CMD_register "New Canvas", "Ctrl+N", CMD_CAT_FILE, 209
    CMD_register "Open Image", "Ctrl+O", CMD_CAT_FILE, 201
    CMD_register "Save Image", "Ctrl+S", CMD_CAT_FILE, 202
    CMD_register "Quick Save", "Ctrl+Shift+S", CMD_CAT_FILE, 203
    CMD_register "Save As...", "Ctrl+Alt+S", CMD_CAT_FILE, 204
    CMD_register "Export Selection", "Ctrl+Alt+Shift+S", CMD_CAT_FILE, 205
    CMD_register "Open Project (.draw)", "Alt+O", CMD_CAT_FILE, 206
    CMD_register "Save Project (.draw)", "Alt+S", CMD_CAT_FILE, 207
    CMD_register "Import Image", "Right-click Open", CMD_CAT_FILE, 208
    CMD_register "Exit", "Alt+X", CMD_CAT_FILE, 212
    
    ' ========== EDIT ==========
    CMD_register "Undo", "Ctrl+Z", CMD_CAT_EDIT, 301
    CMD_register "Redo", "Ctrl+Y", CMD_CAT_EDIT, 302
    CMD_register "Copy", "Ctrl+C", CMD_CAT_EDIT, 303
    CMD_register "Cut", "Ctrl+X", CMD_CAT_EDIT, 304
    CMD_register "Paste", "Ctrl+V", CMD_CAT_EDIT, 305
    CMD_register "Clear Selection", "Ctrl+E", CMD_CAT_EDIT, 306
    CMD_register "Deselect", "Ctrl+D", CMD_CAT_EDIT, 307
    CMD_register "Clear Canvas", "Delete", CMD_CAT_EDIT, 308
    CMD_register "Clear Canvas (No Prompt)", "Backspace", CMD_CAT_EDIT, 309
    CMD_register "Select All", "Ctrl+A", CMD_CAT_EDIT, 310
    CMD_register "Invert Selection", "Ctrl+Shift+I", CMD_CAT_EDIT, 311
    
    ' ========== VIEW ==========
    CMD_register "Toggle Toolbar", "Tab", CMD_CAT_VIEW, 401
    CMD_register "Toggle Status Bar", "F10", CMD_CAT_VIEW, 402
    CMD_register "Toggle All UI", "F11", CMD_CAT_VIEW, 403
    CMD_register "Toggle Layer Panel", "Ctrl+L", CMD_CAT_VIEW, 404
    CMD_register "Reset Zoom", "Ctrl+0", CMD_CAT_VIEW, 405
    CMD_register "Zoom In", "Ctrl+=", CMD_CAT_VIEW, 406
    CMD_register "Zoom Out", "Ctrl+-", CMD_CAT_VIEW, 407
    CMD_register "Display Scale Up", "Ctrl+Alt+Shift+NumPad+", CMD_CAT_VIEW, 408
    CMD_register "Display Scale Down", "Ctrl+Alt+Shift+NumPad-", CMD_CAT_VIEW, 409
    CMD_register "Show Crosshair", "Hold Shift", CMD_CAT_VIEW, 410
    
    ' ========== COLOR ==========
    CMD_register "Opacity 10%", "1", CMD_CAT_COLOR, 501
    CMD_register "Opacity 20%", "2", CMD_CAT_COLOR, 502
    CMD_register "Opacity 30%", "3", CMD_CAT_COLOR, 503
    CMD_register "Opacity 40%", "4", CMD_CAT_COLOR, 504
    CMD_register "Opacity 50%", "5", CMD_CAT_COLOR, 505
    CMD_register "Opacity 60%", "6", CMD_CAT_COLOR, 506
    CMD_register "Opacity 70%", "7", CMD_CAT_COLOR, 507
    CMD_register "Opacity 80%", "8", CMD_CAT_COLOR, 508
    CMD_register "Opacity 90%", "9", CMD_CAT_COLOR, 509
    CMD_register "Opacity 100%", "0", CMD_CAT_COLOR, 510
    CMD_register "Swap FG/BG Colors", "X", CMD_CAT_COLOR, 517
    
    ' ========== BRUSH ==========
    CMD_register "Decrease Brush Size", "[ or {", CMD_CAT_BRUSH, 601
    CMD_register "Increase Brush Size", "] or }", CMD_CAT_BRUSH, 602
    CMD_register "Brush Preset 1", "F1", CMD_CAT_BRUSH, 603
    CMD_register "Brush Preset 2", "F2", CMD_CAT_BRUSH, 604
    CMD_register "Brush Preset 3", "F3", CMD_CAT_BRUSH, 605
    CMD_register "Brush Preset 4", "F4", CMD_CAT_BRUSH, 606
    CMD_register "Toggle Brush Preview", "` or ~", CMD_CAT_BRUSH, 607
    CMD_register "Toggle Brush Shape", "\ or |", CMD_CAT_BRUSH, 608
    CMD_register "Toggle Pixel Perfect", "F6", CMD_CAT_BRUSH, 609
    
    ' ========== LAYER ==========
    CMD_register "New Layer", "Ctrl+Shift+N", CMD_CAT_LAYER, 701
    CMD_register "Delete Layer", "Ctrl+Shift+Delete", CMD_CAT_LAYER, 702
    CMD_register "Move Layer Up", "Ctrl+PgUp", CMD_CAT_LAYER, 703
    CMD_register "Move Layer Down", "Ctrl+PgDn", CMD_CAT_LAYER, 704
    CMD_register "Merge Layer Down", "Ctrl+Alt+E", CMD_CAT_LAYER, 705
    CMD_register "Merge All Visible", "Ctrl+Alt+Shift+E", CMD_CAT_LAYER, 706
    CMD_register "Duplicate Layer", "Ctrl+Shift+D", CMD_CAT_LAYER, 707
    
    ' ========== CANVAS ==========
    CMD_register "Pan Canvas", "Space+Drag or MMB", CMD_CAT_CANVAS, 801
    CMD_register "Reset Pan", "Double MMB", CMD_CAT_CANVAS, 802
    
    ' ========== GRID ==========
    CMD_register "Toggle Grid", "' (apostrophe)", CMD_CAT_GRID, 901
    CMD_register "Toggle Pixel Grid", "Shift+' (quote)", CMD_CAT_GRID, 902
    CMD_register "Toggle Snap to Grid", "; (semicolon)", CMD_CAT_GRID, 903
    CMD_register "Toggle Grid Align Mode", "/ (slash)", CMD_CAT_GRID, 906
    CMD_register "Make Grid Match Brush Size", "Ctrl+Shift+/", CMD_CAT_GRID, 907
    CMD_register "Increase Grid Size", ". (period)", CMD_CAT_GRID, 904
    CMD_register "Decrease Grid Size", ", (comma)", CMD_CAT_GRID, 905
    
    ' ========== SYMMETRY ==========
    CMD_register "Cycle Symmetry Mode", "F7", CMD_CAT_SYMM, 1001
    CMD_register "Clear Symmetry", "F8", CMD_CAT_SYMM, 1002
    CMD_register "Set Symmetry Center", "Ctrl+Click", CMD_CAT_SYMM, 1003
    
    ' ========== CUSTOM BRUSH ==========
    CMD_register "Capture Custom Brush", "Ctrl+B", CMD_CAT_CUSTOM, 1101
    CMD_register "Clear Custom Brush", "Ctrl+B (active)", CMD_CAT_CUSTOM, 1102
    CMD_register "Toggle Recolor Mode", "F9", CMD_CAT_CUSTOM, 1103
    CMD_register "Apply Outline", "Shift+O", CMD_CAT_CUSTOM, 1104
    CMD_register "Flip Brush Horizontal", "Home", CMD_CAT_CUSTOM, 1105
    CMD_register "Flip Brush Vertical", "End", CMD_CAT_CUSTOM, 1106
    CMD_register "Scale Brush Up", "Page Up", CMD_CAT_CUSTOM, 1107
    CMD_register "Scale Brush Down", "Page Down", CMD_CAT_CUSTOM, 1108
    CMD_register "Reset Brush Scale", "/", CMD_CAT_CUSTOM, 1109
    CMD_register "Export Brush as PNG", "F12", CMD_CAT_CUSTOM, 1110
    CMD_register "Rotate Brush 90 CW", ">", CMD_CAT_CUSTOM, 1111
    CMD_register "Rotate Brush 90 CCW", "<", CMD_CAT_CUSTOM, 1112
    
    ' ========== ASSISTANTS ==========
    CMD_register "Constrain H/V", "Hold Shift", CMD_CAT_ASSIST, 1201
    CMD_register "Angle Snap", "Ctrl+Shift", CMD_CAT_ASSIST, 1202
    CMD_register "Draw Square/Circle", "Hold Ctrl", CMD_CAT_ASSIST, 1203
    CMD_register "Draw from Center", "Hold Shift (center)", CMD_CAT_ASSIST, 1204
    CMD_register "Clone Mode (Move)", "Hold Alt", CMD_CAT_ASSIST, 1205
    CMD_register "Temp Color Picker", "Hold Alt", CMD_CAT_ASSIST, 1206
    
    ' Initialize search results to show all
    CMD_update_search
END SUB


''
' Register a command
'
SUB CMD_register (cmd_name AS STRING, cmd_hotkey AS STRING, cmd_category AS INTEGER, cmd_action_id AS INTEGER)
    IF CMD_COUNT >= CMD_MAX_COMMANDS THEN EXIT SUB
    
    CMD_LIST(CMD_COUNT).name = cmd_name$
    CMD_LIST(CMD_COUNT).hotkey = cmd_hotkey$
    CMD_LIST(CMD_COUNT).category = cmd_category%
    CMD_LIST(CMD_COUNT).action_id = cmd_action_id%
    CMD_LIST(CMD_COUNT).enabled = TRUE
    CMD_COUNT = CMD_COUNT + 1
END SUB


''
' Show command palette (with search)
'
SUB CMD_show_palette ()
    CMD_PALETTE.visible = TRUE
    CMD_PALETTE.mode = 0  ' Command palette mode with search
    CMD_PALETTE.search = ""
    CMD_PALETTE.search_len = 0
    CMD_PALETTE.hover = 0
    CMD_PALETTE.scroll = 0
    CMD_update_search
    GUI_NEEDS_REDRAW% = TRUE
END SUB


''
' Show quick reference (no search, just list all)
'
SUB CMD_show_quick_ref ()
    CMD_PALETTE.visible = TRUE
    CMD_PALETTE.mode = 1  ' Quick reference mode
    CMD_PALETTE.search = ""
    CMD_PALETTE.search_len = 0
    CMD_PALETTE.hover = 0
    CMD_PALETTE.scroll = 0
    CMD_update_search
    GUI_NEEDS_REDRAW% = TRUE
END SUB


''
' Hide command palette
'
SUB CMD_hide ()
    CMD_PALETTE.visible = FALSE
    GUI_NEEDS_REDRAW% = TRUE
END SUB


''
' Render command palette
'
SUB CMD_render (dest AS LONG)
    IF NOT CMD_PALETTE.visible THEN EXIT SUB
    
    DIM oldDest AS LONG
    DIM oldFont AS LONG
    DIM i AS INTEGER, idx AS INTEGER
    DIM item_y AS INTEGER
    DIM display_count AS INTEGER
    DIM cat_name AS STRING
    DIM name_str AS STRING, hotkey_str AS STRING
    DIM row_height AS INTEGER
    DIM char_width AS INTEGER
    DIM font_height AS INTEGER
    
    oldDest& = _DEST
    _DEST dest&
    
    ' Set font
    oldFont& = _FONT(dest&)
    IF CMD_FONT_HANDLE > 0 THEN
        _FONT CMD_FONT_HANDLE, dest&
        font_height% = _FONTHEIGHT(CMD_FONT_HANDLE)
        char_width% = _PRINTWIDTH("M", dest&)  ' Use M width for proportional
    ELSE
        font_height% = 8
        char_width% = 8
    END IF
    row_height% = font_height% + 4
    
    ' Recalculate dimensions based on font â€” width scales with canvas
    CMD_PALETTE_W = INT(SCRN.w& * 0.55)
    IF CMD_PALETTE_W < 280 THEN CMD_PALETTE_W = 280
    IF CMD_PALETTE_W > 500 THEN CMD_PALETTE_W = 500
    CMD_PALETTE_H = (CMD_MAX_VISIBLE + 2) * row_height% + 30
    CMD_PALETTE_X = (SCRN.w& - CMD_PALETTE_W) \ 2
    
    ' Draw background with border
    LINE (CMD_PALETTE_X - 2, CMD_PALETTE_Y - 2)-(CMD_PALETTE_X + CMD_PALETTE_W + 2, CMD_PALETTE_Y + CMD_PALETTE_H + 2), _RGB32(0, 0, 0), BF
    LINE (CMD_PALETTE_X - 1, CMD_PALETTE_Y - 1)-(CMD_PALETTE_X + CMD_PALETTE_W + 1, CMD_PALETTE_Y + CMD_PALETTE_H + 1), _RGB32(80, 80, 80), BF
    LINE (CMD_PALETTE_X, CMD_PALETTE_Y)-(CMD_PALETTE_X + CMD_PALETTE_W, CMD_PALETTE_Y + CMD_PALETTE_H), _RGB32(40, 40, 50), BF
    
    ' Draw header
    DIM header_text AS STRING
    IF CMD_PALETTE.mode = 0 THEN
        header_text$ = "Command Palette"
    ELSE
        header_text$ = "Hotkey Quick Reference"
    END IF
    COLOR _RGB32(200, 200, 255), _RGBA32(0, 0, 0, 0)
    _PRINTSTRING (CMD_PALETTE_X + 10, CMD_PALETTE_Y + 6), header_text$
    
    ' Draw close hint
    DIM hint_text AS STRING
    hint_text$ = "ESC close"
    COLOR _RGB32(128, 128, 128), _RGBA32(0, 0, 0, 0)
    _PRINTSTRING (CMD_PALETTE_X + CMD_PALETTE_W - _PRINTWIDTH(hint_text$, dest&) - 10, CMD_PALETTE_Y + 6), hint_text$
    
    ' Draw search box (only in palette mode)
    DIM search_y AS INTEGER
    search_y% = CMD_PALETTE_Y + row_height% + 6
    
    IF CMD_PALETTE.mode = 0 THEN
        LINE (CMD_PALETTE_X + 6, search_y%)-(CMD_PALETTE_X + CMD_PALETTE_W - 6, search_y% + row_height%), _RGB32(30, 30, 40), BF
        LINE (CMD_PALETTE_X + 6, search_y%)-(CMD_PALETTE_X + CMD_PALETTE_W - 6, search_y% + row_height%), _RGB32(100, 100, 120), B
        
        COLOR _RGB32(255, 255, 255), _RGBA32(0, 0, 0, 0)
        IF CMD_PALETTE.search_len > 0 THEN
            _PRINTSTRING (CMD_PALETTE_X + 10, search_y% + 3), LEFT$(CMD_PALETTE.search, CMD_PALETTE.search_len)
        ELSE
            COLOR _RGB32(100, 100, 100), _RGBA32(0, 0, 0, 0)
            _PRINTSTRING (CMD_PALETTE_X + 10, search_y% + 3), "Type to search..."
        END IF
        
        ' Draw cursor
        DIM cursor_x AS INTEGER
        DIM search_text AS STRING
        search_text$ = LEFT$(CMD_PALETTE.search, CMD_PALETTE.search_len)
        cursor_x% = CMD_PALETTE_X + 10 + _PRINTWIDTH(search_text$, dest&)
        LINE (cursor_x%, search_y% + 2)-(cursor_x%, search_y% + row_height% - 2), _RGB32(255, 255, 255)
    END IF
    
    ' Draw results
    DIM list_y AS INTEGER
    IF CMD_PALETTE.mode = 0 THEN
        list_y% = search_y% + row_height% + 4
    ELSE
        list_y% = CMD_PALETTE_Y + row_height% + 6
    END IF
    
    display_count% = CMD_PALETTE.result_count - CMD_PALETTE.scroll
    IF display_count% > CMD_MAX_VISIBLE THEN display_count% = CMD_MAX_VISIBLE
    
    ' Column positions
    DIM cat_col AS INTEGER, name_col AS INTEGER, hotkey_col AS INTEGER
    cat_col% = CMD_PALETTE_X + 10
    name_col% = CMD_PALETTE_X + 80
    hotkey_col% = CMD_PALETTE_X + CMD_PALETTE_W - 10  ' Right edge for right-aligned hotkeys
    
    FOR i% = 0 TO display_count% - 1
        idx% = CMD_RESULTS(CMD_PALETTE.scroll + i%)
        item_y% = list_y% + (i% * row_height%)
        
        ' Highlight hovered item
        IF i% + CMD_PALETTE.scroll = CMD_PALETTE.hover THEN
            LINE (CMD_PALETTE_X + 6, item_y%)-(CMD_PALETTE_X + CMD_PALETTE_W - 6, item_y% + row_height% - 1), _RGB32(60, 80, 120), BF
        END IF
        
        ' Get category name
        cat_name$ = CMD_category_name$(CMD_LIST(idx%).category)
        
        ' Draw category in dim color
        COLOR _RGB32(100, 100, 140), _RGBA32(0, 0, 0, 0)
        _PRINTSTRING (cat_col%, item_y% + 2), LEFT$(cat_name$, 8)
        
        ' Draw command name
        name_str$ = RTRIM$(CMD_LIST(idx%).name)
        COLOR _RGB32(220, 220, 220), _RGBA32(0, 0, 0, 0)
        _PRINTSTRING (name_col%, item_y% + 2), name_str$
        
        ' Draw hotkey on right side (right-aligned)
        hotkey_str$ = RTRIM$(CMD_LIST(idx%).hotkey)
        IF LEN(hotkey_str$) > 0 THEN
            COLOR _RGB32(180, 180, 100), _RGBA32(0, 0, 0, 0)
            _PRINTSTRING (hotkey_col% - _PRINTWIDTH(hotkey_str$, dest&), item_y% + 2), hotkey_str$
        END IF
    NEXT i%
    
    ' Draw scroll indicator if needed
    IF CMD_PALETTE.result_count > CMD_MAX_VISIBLE THEN
        DIM scroll_info AS STRING
        scroll_info$ = _TRIM$(STR$(CMD_PALETTE.scroll + 1)) + "-" + _TRIM$(STR$(CMD_PALETTE.scroll + display_count%)) + " of " + _TRIM$(STR$(CMD_PALETTE.result_count))
        COLOR _RGB32(100, 100, 100), _RGBA32(0, 0, 0, 0)
        _PRINTSTRING (CMD_PALETTE_X + 10, CMD_PALETTE_Y + CMD_PALETTE_H - row_height%), scroll_info$
    END IF
    
    ' Restore font
    IF oldFont& > 0 THEN _FONT oldFont&, dest&
    
    _DEST oldDest&
END SUB


''
' Handle key input for command palette
'
SUB CMD_handle_key (keypress AS STRING)
    IF NOT CMD_PALETTE.visible THEN EXIT SUB
    
    DIM key_code AS INTEGER
    
    IF LEN(keypress$) = 0 THEN EXIT SUB
    
    key_code% = ASC(keypress$)
    
    SELECT CASE key_code%
        CASE 27  ' ESC - close
            CMD_hide
            
        CASE 13  ' Enter - execute selected
            CMD_execute_selected
            CMD_hide
            
        CASE 8   ' Backspace - delete character
            IF CMD_PALETTE.search_len > 0 THEN
                CMD_PALETTE.search_len = CMD_PALETTE.search_len - 1
                CMD_update_search
            END IF
            
        CASE 32 TO 126  ' Printable characters (only in palette mode)
            IF CMD_PALETTE.mode = 0 THEN
                IF CMD_PALETTE.search_len < CMD_MAX_SEARCH THEN
                    MID$(CMD_PALETTE.search, CMD_PALETTE.search_len + 1, 1) = keypress$
                    CMD_PALETTE.search_len = CMD_PALETTE.search_len + 1
                    CMD_update_search
                END IF
            END IF
    END SELECT
    
    GUI_NEEDS_REDRAW% = TRUE
END SUB


''
' Handle mouse click on command palette
'
SUB CMD_handle_click (x AS INTEGER, y AS INTEGER)
    IF NOT CMD_PALETTE.visible THEN EXIT SUB
    IF NOT CMD_in_bounds%(x%, y%) THEN
        CMD_hide
        EXIT SUB
    END IF
    
    ' Calculate row height based on font
    DIM row_height AS INTEGER
    IF CMD_FONT_HANDLE > 0 THEN
        row_height% = _FONTHEIGHT(CMD_FONT_HANDLE) + 4
    ELSE
        row_height% = 14
    END IF
    
    ' Calculate which item was clicked
    DIM list_y AS INTEGER
    DIM item_idx AS INTEGER
    
    IF CMD_PALETTE.mode = 0 THEN
        list_y% = CMD_PALETTE_Y + row_height% + 6 + row_height% + 4  ' After header and search box
    ELSE
        list_y% = CMD_PALETTE_Y + row_height% + 6  ' After header
    END IF
    
    IF y% >= list_y% THEN
        item_idx% = (y% - list_y%) \ row_height% + CMD_PALETTE.scroll
        IF item_idx% >= 0 AND item_idx% < CMD_PALETTE.result_count THEN
            CMD_PALETTE.hover = item_idx%
            CMD_execute_selected
            CMD_hide
        END IF
    END IF
END SUB


''
' Navigate up in list
'
SUB CMD_navigate_up ()
    IF NOT CMD_PALETTE.visible THEN EXIT SUB
    
    IF CMD_PALETTE.hover > 0 THEN
        CMD_PALETTE.hover = CMD_PALETTE.hover - 1
        IF CMD_PALETTE.hover < CMD_PALETTE.scroll THEN
            CMD_PALETTE.scroll = CMD_PALETTE.hover
        END IF
    END IF
    GUI_NEEDS_REDRAW% = TRUE
END SUB


''
' Navigate down in list
'
SUB CMD_navigate_down ()
    IF NOT CMD_PALETTE.visible THEN EXIT SUB
    
    IF CMD_PALETTE.hover < CMD_PALETTE.result_count - 1 THEN
        CMD_PALETTE.hover = CMD_PALETTE.hover + 1
        IF CMD_PALETTE.hover >= CMD_PALETTE.scroll + CMD_MAX_VISIBLE THEN
            CMD_PALETTE.scroll = CMD_PALETTE.hover - CMD_MAX_VISIBLE + 1
        END IF
    END IF
    GUI_NEEDS_REDRAW% = TRUE
END SUB


''
' Navigate page up in list
'
SUB CMD_navigate_page_up ()
    IF NOT CMD_PALETTE.visible THEN EXIT SUB
    
    CMD_PALETTE.hover = CMD_PALETTE.hover - CMD_MAX_VISIBLE
    IF CMD_PALETTE.hover < 0 THEN CMD_PALETTE.hover = 0
    
    CMD_PALETTE.scroll = CMD_PALETTE.scroll - CMD_MAX_VISIBLE
    IF CMD_PALETTE.scroll < 0 THEN CMD_PALETTE.scroll = 0
    
    GUI_NEEDS_REDRAW% = TRUE
END SUB


''
' Navigate page down in list
'
SUB CMD_navigate_page_down ()
    IF NOT CMD_PALETTE.visible THEN EXIT SUB
    
    CMD_PALETTE.hover = CMD_PALETTE.hover + CMD_MAX_VISIBLE
    IF CMD_PALETTE.hover >= CMD_PALETTE.result_count THEN
        CMD_PALETTE.hover = CMD_PALETTE.result_count - 1
    END IF
    
    CMD_PALETTE.scroll = CMD_PALETTE.scroll + CMD_MAX_VISIBLE
    DIM max_scroll AS INTEGER
    max_scroll% = CMD_PALETTE.result_count - CMD_MAX_VISIBLE
    IF max_scroll% < 0 THEN max_scroll% = 0
    IF CMD_PALETTE.scroll > max_scroll% THEN CMD_PALETTE.scroll = max_scroll%
    
    GUI_NEEDS_REDRAW% = TRUE
END SUB


''
' Execute the currently selected command
'
SUB CMD_execute_selected ()
    IF CMD_PALETTE.hover < 0 OR CMD_PALETTE.hover >= CMD_PALETTE.result_count THEN EXIT SUB
    
    DIM idx AS INTEGER
    idx% = CMD_RESULTS(CMD_PALETTE.hover)
    CMD_execute_action CMD_LIST(idx%).action_id
END SUB


''
' Execute a command by action ID
'
SUB CMD_execute_action (action_id AS INTEGER)
    SELECT CASE action_id%
        ' ========== TOOLS ==========
        CASE 101: KEYBOARD_tools "b"  ' Brush
        CASE 102: KEYBOARD_tools "d"  ' Dot
        CASE 103: KEYBOARD_tools "f"  ' Fill
        CASE 104: KEYBOARD_tools "i"  ' Picker
        CASE 105: KEYBOARD_tools "l"  ' Line
        CASE 106: KEYBOARD_tools "p"  ' Polygon
        CASE 107  ' Polygon Filled (simulate Shift+P)
            PREVIOUS_TOOL% = CURRENT_TOOL%
            CURRENT_TOOL% = TOOL_POLYGON_FILLED
            POLY_LINE.FILLED = TRUE
            GUI_NEEDS_REDRAW% = TRUE
        CASE 108: KEYBOARD_tools "r"  ' Rectangle
        CASE 109  ' Rectangle Filled
            PREVIOUS_TOOL% = CURRENT_TOOL%
            CURRENT_TOOL% = TOOL_RECT_FILLED
            GUI_NEEDS_REDRAW% = TRUE
        CASE 110: KEYBOARD_tools "e"  ' Ellipse
        CASE 111  ' Ellipse Filled
            PREVIOUS_TOOL% = CURRENT_TOOL%
            CURRENT_TOOL% = TOOL_ELLIPSE_FILLED
            GUI_NEEDS_REDRAW% = TRUE
        CASE 112: KEYBOARD_tools "m"  ' Marquee
        CASE 117: KEYBOARD_tools "w"  ' Magic Wand
        CASE 113: KEYBOARD_tools "v"  ' Move
        CASE 114: KEYBOARD_tools "t"  ' Text
        CASE 115  ' Text Tiny5
            PREVIOUS_TOOL% = CURRENT_TOOL%
            CURRENT_TOOL% = TOOL_TEXT
            TEXT.USE_CUSTOM = FALSE
            TEXT.USE_TINY5 = TRUE
            TEXT.CHAR_WIDTH = 5
            TEXT.LINE_HEIGHT = 6
            TEXT_reset
            GUI_NEEDS_REDRAW% = TRUE
        CASE 116  ' Text Custom
            PREVIOUS_TOOL% = CURRENT_TOOL%
            CURRENT_TOOL% = TOOL_TEXT
            IF TEXT.CUSTOM_FONT_HANDLE > 0 THEN TEXT.USE_CUSTOM = TRUE
            TEXT.USE_TINY5 = FALSE
            TEXT_reset
            GUI_NEEDS_REDRAW% = TRUE
        
        ' ========== EDIT ==========
        CASE 301: UNDO_undo  ' Undo
        CASE 302: UNDO_redo  ' Redo
        CASE 303: CLIPBOARD_copy  ' Copy
        CASE 304: CLIPBOARD_cut   ' Cut
        CASE 305: CLIPBOARD_paste MOUSE.X%, MOUSE.Y%  ' Paste at mouse position
        CASE 319: CLIPBOARD_copy_merged  ' Copy Merged
        CASE 320  ' Cut to New Layer
            IF CURRENT_LAYER% > 0 THEN
                CLIPBOARD_cut
                DIM ctnl_idx AS INTEGER
                ctnl_idx% = LAYERS_new%
                ' Paste at original source position (center of source region)
                CLIPBOARD_paste CLIPBOARD.SOURCE_X + CLIPBOARD.WIDTH \ 2, CLIPBOARD.SOURCE_Y + CLIPBOARD.HEIGHT \ 2
            END IF
        CASE 321  ' Copy to New Layer
            IF CURRENT_LAYER% > 0 THEN
                CLIPBOARD_copy
                DIM cpnl_idx AS INTEGER
                cpnl_idx% = LAYERS_new%
                ' Paste at original source position (center of source region)
                CLIPBOARD_paste CLIPBOARD.SOURCE_X + CLIPBOARD.WIDTH \ 2, CLIPBOARD.SOURCE_Y + CLIPBOARD.HEIGHT \ 2
            END IF
        CASE 306, 308: CLIPBOARD_clear_selection ' Clear selection / Edit > Clear
        CASE 307: MARQUEE_reset   ' Deselect
        CASE 310: SELECTION_select_all  ' Select All
        CASE 311: SELECTION_invert  ' Invert Selection
        
        ' ========== VIEW ==========
        CASE 401  ' Toggle Toolbar
            SCRN.showToolbar% = NOT SCRN.showToolbar%
            SCRN.toolbarManuallyHidden% = NOT SCRN.showToolbar%
            GUI_NEEDS_REDRAW% = TRUE
            SCENE_DIRTY% = TRUE
        CASE 402  ' Toggle Status Bar
            SCRN.showStatus% = NOT SCRN.showStatus%
            SCRN.statusManuallyHidden% = NOT SCRN.showStatus%
            GUI_NEEDS_REDRAW% = TRUE
            SCENE_DIRTY% = TRUE
        CASE 403  ' Toggle All UI
            IF SCRN.showToolbar% OR SCRN.showStatus% THEN
                SCRN.showToolbar% = FALSE
                SCRN.showStatus% = FALSE
                SCRN.toolbarManuallyHidden% = TRUE
                SCRN.statusManuallyHidden% = TRUE
            ELSE
                SCRN.showToolbar% = TRUE
                SCRN.showStatus% = TRUE
                SCRN.toolbarManuallyHidden% = FALSE
                SCRN.statusManuallyHidden% = FALSE
            END IF
            GUI_NEEDS_REDRAW% = TRUE
            SCENE_DIRTY% = TRUE
        CASE 404  ' Toggle Layer Panel
            LAYER_PANEL.visible% = NOT LAYER_PANEL.visible%
            SCRN.layerPanelManuallyHidden% = NOT LAYER_PANEL.visible%
            GUI_NEEDS_REDRAW% = TRUE
            SCENE_DIRTY% = TRUE
        CASE 405  ' Reset Zoom
            SCRN.zoom! = 1.0
            SCRN.offsetX% = 0
            SCRN.offsetY% = 0
            GUI_NEEDS_REDRAW% = TRUE
            SCENE_DIRTY% = TRUE
        CASE 406  ' Zoom In
            IF SCRN.zoom! < 8.0 THEN
                IF SCRN.zoom! < 1.0 THEN
                    SCRN.zoom! = 1.0
                ELSE
                    SCRN.zoom! = SCRN.zoom! + 1.0
                END IF
                GUI_NEEDS_REDRAW% = TRUE
                SCENE_DIRTY% = TRUE
            END IF
        CASE 407  ' Zoom Out
            IF SCRN.zoom! > 0.25 THEN
                IF SCRN.zoom! > 1.0 THEN
                    SCRN.zoom! = SCRN.zoom! - 1.0
                ELSEIF SCRN.zoom! = 1.0 THEN
                    SCRN.zoom! = 0.5
                ELSE
                    SCRN.zoom! = 0.25
                END IF
                GUI_NEEDS_REDRAW% = TRUE
                SCENE_DIRTY% = TRUE
            END IF
        
        ' ========== COLOR / OPACITY ==========
        CASE 501: PAINT_OPACITY% = 10:  GUI_NEEDS_REDRAW% = TRUE
        CASE 502: PAINT_OPACITY% = 20:  GUI_NEEDS_REDRAW% = TRUE
        CASE 503: PAINT_OPACITY% = 30:  GUI_NEEDS_REDRAW% = TRUE
        CASE 504: PAINT_OPACITY% = 40:  GUI_NEEDS_REDRAW% = TRUE
        CASE 505: PAINT_OPACITY% = 50:  GUI_NEEDS_REDRAW% = TRUE
        CASE 506: PAINT_OPACITY% = 60:  GUI_NEEDS_REDRAW% = TRUE
        CASE 507: PAINT_OPACITY% = 70:  GUI_NEEDS_REDRAW% = TRUE
        CASE 508: PAINT_OPACITY% = 80:  GUI_NEEDS_REDRAW% = TRUE
        CASE 509: PAINT_OPACITY% = 90:  GUI_NEEDS_REDRAW% = TRUE
        CASE 510: PAINT_OPACITY% = 100: GUI_NEEDS_REDRAW% = TRUE

        ' ========== BRUSH ==========
        CASE 601: BRUSH_SIZE_decrease
        CASE 602: BRUSH_SIZE_increase
        CASE 603: BRUSH_SIZE_set_preset 1
        CASE 604: BRUSH_SIZE_set_preset 2
        CASE 605: BRUSH_SIZE_set_preset 3
        CASE 606: BRUSH_SIZE_set_preset 4
        CASE 607: BRUSH_SIZE_toggle_preview: GUI_NEEDS_REDRAW% = TRUE: FRAME_IDLE% = FALSE
        CASE 608: BRUSH_SIZE_toggle_shape: GUI_NEEDS_REDRAW% = TRUE: FRAME_IDLE% = FALSE
        CASE 609: BRUSH_SIZE_toggle_pixel_perfect: GUI_NEEDS_REDRAW% = TRUE: FRAME_IDLE% = FALSE
        
        ' ========== LAYER ==========
        CASE 701  ' New Layer
            DIM newLayerIdx AS INTEGER
            newLayerIdx% = LAYERS_new%
        CASE 702: LAYERS_delete CURRENT_LAYER%  ' Delete Layer
        CASE 703: LAYERS_move_up CURRENT_LAYER%
        CASE 704: LAYERS_move_down CURRENT_LAYER%
        CASE 705: LAYERS_merge_down CURRENT_LAYER%
        CASE 706: LAYERS_merge_visible
        CASE 707: LAYERS_duplicate CURRENT_LAYER%  ' Duplicate Layer
        
        ' ========== GRID ==========
        CASE 901: GRID.SHOW% = NOT GRID.SHOW%: GUI_NEEDS_REDRAW% = TRUE: SCENE_DIRTY% = TRUE
        CASE 902: PIXEL_GRID.SHOW% = NOT PIXEL_GRID.SHOW%: GUI_NEEDS_REDRAW% = TRUE: SCENE_DIRTY% = TRUE
        CASE 903: GRID.SNAP% = NOT GRID.SNAP%: GUI_NEEDS_REDRAW% = TRUE
        CASE 904  ' Increase grid size
            IF GRID.gridWidth% < 50 THEN GRID.gridWidth% = GRID.gridWidth% + 1: GRID.gridHeight% = GRID.gridWidth%: GRID_draw: GUI_NEEDS_REDRAW% = TRUE: SCENE_DIRTY% = TRUE
        CASE 905  ' Decrease grid size
            IF GRID.gridWidth% > 2 THEN GRID.gridWidth% = GRID.gridWidth% - 1: GRID.gridHeight% = GRID.gridWidth%: GRID_draw: GUI_NEEDS_REDRAW% = TRUE: SCENE_DIRTY% = TRUE
        CASE 906  ' Toggle grid tile mode (ON = center/tiling, OFF = corner/lines)
            IF GRID.ALIGN_MODE% = GRID_ALIGN_CORNER THEN GRID.ALIGN_MODE% = GRID_ALIGN_CENTER ELSE GRID.ALIGN_MODE% = GRID_ALIGN_CORNER
            GUI_NEEDS_REDRAW% = TRUE
        CASE 908  ' Toggle grid cell fill mode
            GRID.CELL_FILL% = NOT GRID.CELL_FILL%: GUI_NEEDS_REDRAW% = TRUE
        CASE 907  ' Make grid match brush size
            IF CUSTOM_BRUSH_is_active% THEN
                ' Use custom brush dimensions
                GRID.gridWidth% = CUSTOM_BRUSH.WIDTH%
                GRID.gridHeight% = CUSTOM_BRUSH.HEIGHT%
            ELSE
                ' Use regular brush size
                GRID.gridWidth% = BRUSH_PIXEL_SIZES(BRUSH_SIZE.CURRENT)
                GRID.gridHeight% = BRUSH_PIXEL_SIZES(BRUSH_SIZE.CURRENT)
            END IF
            GRID.ALIGN_MODE% = GRID_ALIGN_CENTER  ' Center/tile mode for filling gaps
            GRID.SNAP% = TRUE  ' Enable snap to grid
            GRID_draw
            GUI_NEEDS_REDRAW% = TRUE
            SCENE_DIRTY% = TRUE
        
        ' ========== SYMMETRY ==========
        CASE 1001: SYMMETRY_cycle_mode
        CASE 1002: SYMMETRY_clear
        
        ' ========== CUSTOM BRUSH ==========
        CASE 1103  ' Toggle recolor
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_toggle_recolor
        CASE 1104  ' Outline with BG color
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_apply_outline
        CASE 1105  ' Flip H
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_toggle_flip_h
        CASE 1106  ' Flip V
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_toggle_flip_v
        CASE 1107  ' Scale up
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_scale_up
        CASE 1108  ' Scale down
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_scale_down
        CASE 1109  ' Reset scale
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_scale_reset
        CASE 1111  ' Rotate 90 CW
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_rotate_cw
        CASE 1112  ' Rotate 90 CCW
            IF CUSTOM_BRUSH_is_active% THEN CUSTOM_BRUSH_rotate_ccw
        
        ' ========== FILE (menu-only) ==========
        CASE 202  ' Save (Ctrl+S)
            SAVE_image
        CASE 203  ' Save as PNG (Ctrl+Shift+S)
            SAVE_quick
        CASE 206  ' Open DRAW file
            DRW_open_dialog
        CASE 207  ' Save as DRAW (Alt+S)
            DRW_save_dialog
        CASE 208  ' Import Image
            IMPORT_image
        CASE 209  ' New
            DRW_new_canvas
        CASE 210  ' New from Template
            ERR_info "New from template: not yet implemented"
        CASE 211  ' Export Layer
            DIM layerExportPath AS STRING
            POINTER_hide_for_dialog
            layerExportPath$ = _SAVEFILEDIALOG$("Export Layer as PNG", "", "*.png", "PNG Image")
            POINTER_show_after_dialog
            IF layerExportPath$ <> "" THEN
                ' Save the current layer's image handle as PNG
                IF CURRENT_LAYER% > 0 AND CURRENT_LAYER% <= LAYER_COUNT% THEN
                    IF LAYERS(CURRENT_LAYER%).imgHandle& < -1 THEN
                        _SAVEIMAGE layerExportPath$, LAYERS(CURRENT_LAYER%).imgHandle&
                        _LOGINFO "Layer exported to: " + layerExportPath$
                    ELSE
                        ERR_info "Invalid layer image"
                    END IF
                ELSE
                    ERR_info "No valid layer selected"
                END IF
            END IF
        CASE 212  ' Exit
            DIM shouldExit AS INTEGER
            shouldExit% = TRUE
            IF CANVAS_DIRTY% THEN
                POINTER_hide_for_dialog
                DIM exitResult AS LONG
                exitResult& = _MESSAGEBOX("DRAW", "Canvas has unsaved changes. Exit anyway?", "yesno", "question", 0)
                POINTER_show_after_dialog
                IF exitResult& <> 1 THEN shouldExit% = FALSE
            END IF
            IF shouldExit% THEN SYSTEM
        
        ' ========== EDIT (menu-only) ==========
        CASE 312  ' Paste in Place
            ' Paste back at the original source position (center of source region)
            CLIPBOARD_paste CLIPBOARD.SOURCE_X + CLIPBOARD.WIDTH \ 2, CLIPBOARD.SOURCE_Y + CLIPBOARD.HEIGHT \ 2
        CASE 313  ' Fill with FG
            IF CURRENT_LAYER% > 0 THEN
                DIM oldFillDest AS LONG
                oldFillDest& = _DEST
                STROKE_begin
                _DEST LAYERS(CURRENT_LAYER%).imgHandle&
                IF MARQUEE.ACTIVE AND MARQUEE.USER_CREATED AND MARQUEE.INITIALIZED THEN
                    LINE (MARQUEE.BOX.x, MARQUEE.BOX.y)-(MARQUEE.BOX.x + MARQUEE.BOX.w - 1, MARQUEE.BOX.y + MARQUEE.BOX.h - 1), PAINT_COLOR~&, BF
                ELSE
                    CLS , PAINT_COLOR~&
                END IF
                _DEST oldFillDest&
                STROKE_commit
                IF NOT UNDO_saved_this_frame% THEN
                    UNDO_save_state
                    UNDO_saved_this_frame% = TRUE
                END IF
                LAYERS(CURRENT_LAYER%).contentDirty% = TRUE
                BLEND_invalidate_cache
                CANVAS_DIRTY% = TRUE
                SCENE_DIRTY% = TRUE
                GUI_NEEDS_REDRAW% = TRUE
                FRAME_IDLE% = FALSE
            END IF
        CASE 314  ' Fill with BG
            IF CURRENT_LAYER% > 0 THEN
                DIM oldBgDest AS LONG
                oldBgDest& = _DEST
                _DEST LAYERS(CURRENT_LAYER%).imgHandle&
                IF MARQUEE.ACTIVE AND MARQUEE.USER_CREATED AND MARQUEE.INITIALIZED THEN
                    LINE (MARQUEE.BOX.x, MARQUEE.BOX.y)-(MARQUEE.BOX.x + MARQUEE.BOX.w - 1, MARQUEE.BOX.y + MARQUEE.BOX.h - 1), PAINT_BG_COLOR~&, BF
                ELSE
                    CLS , PAINT_BG_COLOR~&
                END IF
                _DEST oldBgDest&
                IF NOT UNDO_saved_this_frame% THEN
                    UNDO_save_state
                    UNDO_saved_this_frame% = TRUE
                END IF
                LAYERS(CURRENT_LAYER%).contentDirty% = TRUE
                BLEND_invalidate_cache
                CANVAS_DIRTY% = TRUE
                SCENE_DIRTY% = TRUE
                GUI_NEEDS_REDRAW% = TRUE
                FRAME_IDLE% = FALSE
            END IF
        CASE 315  ' Flip Horizontal
            IF CUSTOM_BRUSH_is_active% THEN
                CUSTOM_BRUSH_toggle_flip_h
            ELSEIF CURRENT_TOOL% = TOOL_MOVE AND MOVE.ACTIVE AND MOVE.SELECTION_IMAGE < -1 THEN
                ' Flip the floating move selection horizontally
                DIM fhMoveW AS INTEGER, fhMoveH AS INTEGER
                fhMoveW% = _WIDTH(MOVE.SELECTION_IMAGE)
                fhMoveH% = _HEIGHT(MOVE.SELECTION_IMAGE)
                DIM fhMoveTmp AS LONG
                fhMoveTmp& = _NEWIMAGE(fhMoveW%, fhMoveH%, 32)
                _PUTIMAGE (fhMoveW% - 1, 0)-(0, fhMoveH% - 1), MOVE.SELECTION_IMAGE, fhMoveTmp&
                _DEST MOVE.SELECTION_IMAGE
                _DONTBLEND MOVE.SELECTION_IMAGE
                _PUTIMAGE , fhMoveTmp&, MOVE.SELECTION_IMAGE
                _BLEND MOVE.SELECTION_IMAGE
                _DEST SCRN.CANVAS&
                IF fhMoveTmp& < -1 THEN _FREEIMAGE fhMoveTmp&
                MOVE_update_preview_buffer
                SCENE_DIRTY% = TRUE
                GUI_NEEDS_REDRAW% = TRUE
                FRAME_IDLE% = FALSE
            ELSEIF CURRENT_LAYER% > 0 THEN
                DIM flipH_src AS LONG, flipH_w AS LONG, flipH_h AS LONG
                flipH_src& = LAYERS(CURRENT_LAYER%).imgHandle&
                IF MARQUEE.ACTIVE AND MARQUEE.USER_CREATED AND MARQUEE.INITIALIZED THEN
                    ' Flip only within selection bounds
                    DIM fhSelW AS INTEGER, fhSelH AS INTEGER
                    fhSelW% = MARQUEE.BOX.w : fhSelH% = MARQUEE.BOX.h
                    DIM fhRegion AS LONG
                    fhRegion& = _NEWIMAGE(fhSelW%, fhSelH%, 32)
                    _PUTIMAGE (0, 0), flipH_src&, fhRegion&, (MARQUEE.BOX.x, MARQUEE.BOX.y)-(MARQUEE.BOX.x + fhSelW% - 1, MARQUEE.BOX.y + fhSelH% - 1)
                    DIM oldFHSelDest AS LONG : oldFHSelDest& = _DEST
                    _DEST flipH_src&
                    _DONTBLEND flipH_src&
                    _PUTIMAGE (MARQUEE.BOX.x + fhSelW% - 1, MARQUEE.BOX.y)-(MARQUEE.BOX.x, MARQUEE.BOX.y + fhSelH% - 1), fhRegion&, flipH_src&
                    _BLEND flipH_src&
                    _DEST oldFHSelDest&
                    IF fhRegion& < -1 THEN _FREEIMAGE fhRegion&
                ELSE
                    ' Flip entire layer
                    flipH_w& = _WIDTH(flipH_src&)
                    flipH_h& = _HEIGHT(flipH_src&)
                    DIM flipH_tmp AS LONG
                    flipH_tmp& = _NEWIMAGE(flipH_w&, flipH_h&, 32)
                    _PUTIMAGE (flipH_w& - 1, 0)-(0, flipH_h& - 1), flipH_src&, flipH_tmp&
                    DIM oldFlipDest AS LONG : oldFlipDest& = _DEST
                    _DEST flipH_src&
                    _DONTBLEND flipH_src&
                    _PUTIMAGE , flipH_tmp&, flipH_src&
                    _BLEND flipH_src&
                    _DEST oldFlipDest&
                    IF flipH_tmp& < -1 THEN _FREEIMAGE flipH_tmp&
                END IF
                IF NOT UNDO_saved_this_frame% THEN
                    UNDO_save_state
                    UNDO_saved_this_frame% = TRUE
                END IF
                LAYERS(CURRENT_LAYER%).contentDirty% = TRUE
                BLEND_invalidate_cache
                CANVAS_DIRTY% = TRUE
                SCENE_DIRTY% = TRUE
                GUI_NEEDS_REDRAW% = TRUE
                FRAME_IDLE% = FALSE
            END IF
        CASE 316  ' Flip Vertical
            IF CUSTOM_BRUSH_is_active% THEN
                CUSTOM_BRUSH_toggle_flip_v
            ELSEIF CURRENT_TOOL% = TOOL_MOVE AND MOVE.ACTIVE AND MOVE.SELECTION_IMAGE < -1 THEN
                ' Flip the floating move selection vertically
                DIM fvMoveW AS INTEGER, fvMoveH AS INTEGER
                fvMoveW% = _WIDTH(MOVE.SELECTION_IMAGE)
                fvMoveH% = _HEIGHT(MOVE.SELECTION_IMAGE)
                DIM fvMoveTmp AS LONG
                fvMoveTmp& = _NEWIMAGE(fvMoveW%, fvMoveH%, 32)
                _PUTIMAGE (0, fvMoveH% - 1)-(fvMoveW% - 1, 0), MOVE.SELECTION_IMAGE, fvMoveTmp&
                _DEST MOVE.SELECTION_IMAGE
                _DONTBLEND MOVE.SELECTION_IMAGE
                _PUTIMAGE , fvMoveTmp&, MOVE.SELECTION_IMAGE
                _BLEND MOVE.SELECTION_IMAGE
                _DEST SCRN.CANVAS&
                IF fvMoveTmp& < -1 THEN _FREEIMAGE fvMoveTmp&
                MOVE_update_preview_buffer
                SCENE_DIRTY% = TRUE
                GUI_NEEDS_REDRAW% = TRUE
                FRAME_IDLE% = FALSE
            ELSEIF CURRENT_LAYER% > 0 THEN
                DIM flipV_src AS LONG, flipV_w AS LONG, flipV_h AS LONG
                flipV_src& = LAYERS(CURRENT_LAYER%).imgHandle&
                IF MARQUEE.ACTIVE AND MARQUEE.USER_CREATED AND MARQUEE.INITIALIZED THEN
                    ' Flip only within selection bounds
                    DIM fvSelW AS INTEGER, fvSelH AS INTEGER
                    fvSelW% = MARQUEE.BOX.w : fvSelH% = MARQUEE.BOX.h
                    DIM fvRegion AS LONG
                    fvRegion& = _NEWIMAGE(fvSelW%, fvSelH%, 32)
                    _PUTIMAGE (0, 0), flipV_src&, fvRegion&, (MARQUEE.BOX.x, MARQUEE.BOX.y)-(MARQUEE.BOX.x + fvSelW% - 1, MARQUEE.BOX.y + fvSelH% - 1)
                    DIM oldFVSelDest AS LONG : oldFVSelDest& = _DEST
                    _DEST flipV_src&
                    _DONTBLEND flipV_src&
                    _PUTIMAGE (MARQUEE.BOX.x, MARQUEE.BOX.y + fvSelH% - 1)-(MARQUEE.BOX.x + fvSelW% - 1, MARQUEE.BOX.y), fvRegion&, flipV_src&
                    _BLEND flipV_src&
                    _DEST oldFVSelDest&
                    IF fvRegion& < -1 THEN _FREEIMAGE fvRegion&
                ELSE
                    ' Flip entire layer
                    flipV_w& = _WIDTH(flipV_src&)
                    flipV_h& = _HEIGHT(flipV_src&)
                    DIM flipV_tmp AS LONG
                    flipV_tmp& = _NEWIMAGE(flipV_w&, flipV_h&, 32)
                    _PUTIMAGE (0, flipV_h& - 1)-(flipV_w& - 1, 0), flipV_src&, flipV_tmp&
                    DIM oldFlipVDest AS LONG : oldFlipVDest& = _DEST
                    _DEST flipV_src&
                    _DONTBLEND flipV_src&
                    _PUTIMAGE , flipV_tmp&, flipV_src&
                    _BLEND flipV_src&
                    _DEST oldFlipVDest&
                    IF flipV_tmp& < -1 THEN _FREEIMAGE flipV_tmp&
                END IF
                IF NOT UNDO_saved_this_frame% THEN
                    UNDO_save_state
                    UNDO_saved_this_frame% = TRUE
                END IF
                LAYERS(CURRENT_LAYER%).contentDirty% = TRUE
                BLEND_invalidate_cache
                CANVAS_DIRTY% = TRUE
                SCENE_DIRTY% = TRUE
                GUI_NEEDS_REDRAW% = TRUE
                FRAME_IDLE% = FALSE
            END IF
        CASE 317  ' Scale -50%
            IF CUSTOM_BRUSH_is_active% THEN
                CUSTOM_BRUSH_scale_down
            ELSE
                ERR_info "Scale -50%: not yet implemented"
            END IF
        CASE 318  ' Scale +50%
            IF CUSTOM_BRUSH_is_active% THEN
                CUSTOM_BRUSH_scale_up
            ELSE
                ERR_info "Scale +50%: not yet implemented"
            END IF
        
        ' ========== VIEW (menu-only) ==========
        CASE 411  ' Toggle Menu Bar
            SCRN.showMenubar% = NOT SCRN.showMenubar%
            MENU_BAR.visible% = SCRN.showMenubar%
            IF NOT SCRN.showMenubar% THEN
                SCRN.menubarManuallyHidden% = TRUE
                MENUBAR_close_all
            ELSE
                SCRN.menubarManuallyHidden% = FALSE
            END IF
            GUI_NEEDS_REDRAW% = TRUE
            SCENE_DIRTY% = TRUE
        CASE 412  ' Toggle Cursors
            POINTER.HIDDEN = NOT POINTER.HIDDEN
            GUI_NEEDS_REDRAW% = TRUE
        CASE 413  ' Reset Canvas Position
            SCRN.zoom! = 1.0
            SCRN.offsetX% = 0
            SCRN.offsetY% = 0
            SCENE_DIRTY% = TRUE
            GUI_NEEDS_REDRAW% = TRUE
        
        ' ========== LAYER (menu-only) ==========
        CASE 708  ' Arrange to Top
            DIM arrI AS INTEGER
            FOR arrI% = 1 TO LAYER_COUNT%
                IF CURRENT_LAYER% > 0 THEN LAYERS_move_up CURRENT_LAYER%
            NEXT arrI%
        CASE 709  ' Arrange to Bottom
            DIM arrJ AS INTEGER
            FOR arrJ% = 1 TO LAYER_COUNT%
                IF CURRENT_LAYER% > 0 THEN LAYERS_move_down CURRENT_LAYER%
            NEXT arrJ%
        CASE 710  ' Export Layer as PNG
            ERR_info "Export layer as PNG: not yet implemented"
        
        ' ========== SELECT (menu-only) ==========
        CASE 801  ' Pan tool (space emulation)
            ERR_info "Pan mode: hold Space and drag on canvas"
        CASE 1401  ' Select from current layer
            LAYER_select_non_transparent CURRENT_LAYER%
        CASE 1402  ' Nudge 1px left
            IF MARQUEE.ACTIVE% THEN MARQUEE.BOX.x = MARQUEE.BOX.x - 1
        CASE 1403  ' Nudge 1px right
            IF MARQUEE.ACTIVE% THEN MARQUEE.BOX.x = MARQUEE.BOX.x + 1
        CASE 1404  ' Nudge 1px down
            IF MARQUEE.ACTIVE% THEN MARQUEE.BOX.y = MARQUEE.BOX.y + 1
        CASE 1405  ' Nudge 1px up
            IF MARQUEE.ACTIVE% THEN MARQUEE.BOX.y = MARQUEE.BOX.y - 1
        CASE 1406  ' Nudge 10px left
            IF MARQUEE.ACTIVE% THEN MARQUEE.BOX.x = MARQUEE.BOX.x - 10
        CASE 1407  ' Nudge 10px right
            IF MARQUEE.ACTIVE% THEN MARQUEE.BOX.x = MARQUEE.BOX.x + 10
        CASE 1408  ' Nudge 10px down
            IF MARQUEE.ACTIVE% THEN MARQUEE.BOX.y = MARQUEE.BOX.y + 10
        CASE 1409  ' Nudge 10px up
            IF MARQUEE.ACTIVE% THEN MARQUEE.BOX.y = MARQUEE.BOX.y - 10
        
        ' ========== PALETTE (menu-only) ==========
        CASE 1501  ' Import palette
            ERR_info "Import palette: not yet implemented"
        CASE 1502  ' Export palette
            ERR_info "Export palette: not yet implemented"
        CASE 1503  ' Random palette
            ERR_info "Random palette: not yet implemented"
        CASE 1504  ' Swap FG/BG Colors
            DIM swap_tmp_color AS _UNSIGNED LONG
            DIM swap_tmp_idx AS INTEGER
            swap_tmp_color~& = PAINT_COLOR~&
            swap_tmp_idx% = PAL_FG_IDX%
            PAINT_COLOR~& = PAINT_BG_COLOR~&
            PAL_FG_IDX% = PAL_BG_IDX%
            PAINT_BG_COLOR~& = swap_tmp_color~&
            PAL_BG_IDX% = swap_tmp_idx%
            GUI_NEEDS_REDRAW% = TRUE
        CASE 1505  ' Color Picker dialog for FG
            DIM picker_color AS _UNSIGNED LONG
            picker_color~& = PALETTE_PICKER_show~&("Choose Foreground Color", PAINT_COLOR~&)
            IF picker_color~& <> PAINT_COLOR~& THEN
                PAL_FG_IS_TRANSPARENT% = FALSE
                PAINT_COLOR~& = picker_color~&
                POINTER.C~& = picker_color~&
                DIM picker_pal_idx AS INTEGER
                picker_pal_idx% = PAL_find_index%(picker_color~&)
                IF picker_pal_idx% >= 0 THEN PAL_FG_IDX% = picker_pal_idx%
                CANVAS_DIRTY% = TRUE
                GUI_NEEDS_REDRAW% = TRUE
            END IF
            MOUSE_reset_buttons
        
        ' ========== TOOLS (menu-only) ==========
        CASE 1102  ' Clear custom brush
            IF CUSTOM_BRUSH_is_active% THEN
                CUSTOM_BRUSH_reset
                GUI_NEEDS_REDRAW% = TRUE
            END IF
        CASE 1110  ' Export brush as PNG
            IF CUSTOM_BRUSH_is_active% THEN
                DIM brushExportPath AS STRING
                POINTER_hide_for_dialog
                brushExportPath$ = _SAVEFILEDIALOG$("Export Brush as PNG", "", "*.png", "PNG Image")
                POINTER_show_after_dialog
                IF brushExportPath$ <> "" THEN
                    CUSTOM_BRUSH_export_png brushExportPath$
                END IF
            ELSE
                ERR_info "No custom brush active. Create one with Ctrl+B first."
            END IF
        CASE 1701  ' Zoom tool
            ERR_info "Zoom: use Ctrl+= / Ctrl+- or scroll wheel"
        CASE 1702  ' Spray tool
            PREVIOUS_TOOL% = CURRENT_TOOL%
            CURRENT_TOOL% = TOOL_BRUSH  ' Spray is a brush variant
            GUI_NEEDS_REDRAW% = TRUE
        CASE 1703  ' Command palette / cheat sheet
            CMD_show_quick_ref
        CASE 1704  ' Code / BAS export
            ERR_info "Code view: not yet implemented"
        
        ' ========== HELP ==========
        CASE 1601  ' About
            POINTER_hide_for_dialog
            DIM aboutMsg AS STRING
            aboutMsg$ = "DRAW " + APP_VERSION$ + CHR$(10) + CHR$(10) + _
                        "A pixel art editor by grymmjack (Rick Christy)" + CHR$(10) + _
                        "Written in QB64-PE" + CHR$(10) + CHR$(10) + _
                        "https://github.com/grymmjack/DRAW"
            DIM aboutResult AS LONG
            aboutResult& = _MESSAGEBOX("About DRAW", aboutMsg$, "ok", "info", 0)
            POINTER_show_after_dialog
        CASE 1602  ' Cheat sheet (same as command palette quick ref)
            CMD_show_quick_ref
        CASE 1603  ' Manual
            ERR_info "Manual: not yet available"
        CASE 1604  ' GitHub
            SHELL "xdg-open https://github.com/grymmjack/DRAW &"
        CASE 1605  ' Issue tracker
            SHELL "xdg-open https://github.com/grymmjack/DRAW/issues &"
        CASE 1606  ' Credits
            POINTER_hide_for_dialog
            DIM creditsMsg AS STRING
            creditsMsg$ = "DRAW " + APP_VERSION$ + CHR$(10) + CHR$(10) + _
                          "Created by grymmjack (Rick Christy)" + CHR$(10) + _
                          "github.com/grymmjack" + CHR$(10) + CHR$(10) + _
                          "Built with QB64-PE" + CHR$(10) + _
                          "github.com/QB64-Phoenix-Edition"
            DIM creditsResult AS LONG
            creditsResult& = _MESSAGEBOX("Credits", creditsMsg$, "ok", "info", 0)
            POINTER_show_after_dialog
        CASE 1607  ' Examples
            ERR_info "Examples: not yet available"
    END SELECT
END SUB


''
' Update search results based on current search string
'
SUB CMD_update_search ()
    DIM i AS INTEGER
    DIM search_str AS STRING
    DIM name_str AS STRING
    
    CMD_PALETTE.result_count = 0
    search_str$ = UCASE$(LEFT$(CMD_PALETTE.search, CMD_PALETTE.search_len))
    
    FOR i% = 0 TO CMD_COUNT - 1
        ' If no search, show all
        IF CMD_PALETTE.search_len = 0 THEN
            CMD_RESULTS(CMD_PALETTE.result_count) = i%
            CMD_PALETTE.result_count = CMD_PALETTE.result_count + 1
        ELSE
            ' Check for fuzzy match in name or hotkey
            name_str$ = UCASE$(RTRIM$(CMD_LIST(i%).name))
            IF CMD_fuzzy_match%(search_str$, name_str$) THEN
                CMD_RESULTS(CMD_PALETTE.result_count) = i%
                CMD_PALETTE.result_count = CMD_PALETTE.result_count + 1
            ELSEIF INSTR(UCASE$(RTRIM$(CMD_LIST(i%).hotkey)), search_str$) > 0 THEN
                CMD_RESULTS(CMD_PALETTE.result_count) = i%
                CMD_PALETTE.result_count = CMD_PALETTE.result_count + 1
            END IF
        END IF
    NEXT i%
    
    ' Reset hover and scroll
    CMD_PALETTE.hover = 0
    CMD_PALETTE.scroll = 0
END SUB


''
' Fuzzy match - check if search characters appear in order in target
'
FUNCTION CMD_fuzzy_match% (searchStr AS STRING, targetStr AS STRING)
    DIM si AS INTEGER, ti AS INTEGER
    DIM sc AS STRING, tc AS STRING
    
    IF LEN(searchStr$) = 0 THEN
        CMD_fuzzy_match% = TRUE
        EXIT FUNCTION
    END IF
    
    si% = 1
    ti% = 1
    
    DO WHILE si% <= LEN(searchStr$) AND ti% <= LEN(targetStr$)
        sc$ = MID$(searchStr$, si%, 1)
        tc$ = MID$(targetStr$, ti%, 1)
        
        IF sc$ = tc$ THEN
            si% = si% + 1
        END IF
        ti% = ti% + 1
    LOOP
    
    CMD_fuzzy_match% = (si% > LEN(searchStr$))
END FUNCTION


''
' Check if coordinates are within command palette bounds
'
FUNCTION CMD_in_bounds% (x AS INTEGER, y AS INTEGER)
    CMD_in_bounds% = (x% >= CMD_PALETTE_X AND x% <= CMD_PALETTE_X + CMD_PALETTE_W AND _
                      y% >= CMD_PALETTE_Y AND y% <= CMD_PALETTE_Y + CMD_PALETTE_H)
END FUNCTION


''
' Get category name from category constant
'
FUNCTION CMD_category_name$ (cat AS INTEGER)
    SELECT CASE cat%
        CASE CMD_CAT_TOOL: CMD_category_name$ = "Tool"
        CASE CMD_CAT_FILE: CMD_category_name$ = "File"
        CASE CMD_CAT_EDIT: CMD_category_name$ = "Edit"
        CASE CMD_CAT_VIEW: CMD_category_name$ = "View"
        CASE CMD_CAT_COLOR: CMD_category_name$ = "Color"
        CASE CMD_CAT_BRUSH: CMD_category_name$ = "Brush"
        CASE CMD_CAT_LAYER: CMD_category_name$ = "Layer"
        CASE CMD_CAT_CANVAS: CMD_category_name$ = "Canvas"
        CASE CMD_CAT_ASSIST: CMD_category_name$ = "Assist"
        CASE CMD_CAT_CUSTOM: CMD_category_name$ = "Custom"
        CASE CMD_CAT_GRID: CMD_category_name$ = "Grid"
        CASE CMD_CAT_SYMM: CMD_category_name$ = "Symmetry"
        CASE CMD_CAT_SELECT: CMD_category_name$ = "Select"
        CASE CMD_CAT_HELP: CMD_category_name$ = "Help"
        CASE ELSE: CMD_category_name$ = "Other"
    END SELECT
END FUNCTION
