''
' DRAW - GUI/STATUS.BM
' =============================================================================
' Status subs and functions.
'
' @todo
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Renders the status
' 
SUB STATUS_render ()
    DIM s AS STRING
    DIM AS INTEGER px, py, pw, ph
    DIM tool_name AS STRING
    s$ = ""
    
    ' Add tool name
    SELECT CASE CURRENT_TOOL%
        CASE TOOL_BRUSH: tool_name$ = "BRUSH"
        CASE TOOL_FILL:  tool_name$ = "FILL"
        CASE TOOL_DOT:   tool_name$ = "DOT"
        CASE TOOL_LINE:  tool_name$ = "LINE"
        CASE ELSE:       tool_name$ = "UNKNOWN"
    END SELECT
    s$ = s$ + tool_name$
    
    ' Add brush size and shape if brush tool is active
    IF CURRENT_TOOL% = TOOL_BRUSH THEN
        s$ = s$ + ":" + ns$(BRUSH_SIZE.CURRENT)
        IF BRUSH_SIZE.SHAPE = 0 THEN
            s$ = s$ + CHR$(9)  ' Circle: ○
        ELSE
            s$ = s$ + CHR$(254) ' Square: ■
        END IF
    END IF
    
    pw% = _PRINTWIDTH(s$)
    ph% = _FONTHEIGHT + 1
    px% = 2 : py% = SCRN.h& - ph% + 1
    s$ = s$ + "  " + ns$(MOUSE.X%) + "," + ns$(MOUSE.Y%)
    IF MOUSE.B1% THEN s$ = s$ + " B1"
    IF MOUSE.B2% THEN s$ = s$ + " B2"
    IF MOUSE.B3% THEN s$ = s$ + " B3"
    IF _KEYDOWN(KEY_CAPSLOCK&) THEN s$ = s$ + " " + CHR$(30)
    IF MOUSE.DRAG$ <> "" THEN s$ = s$ + " D" + MOUSE.DRAG$
    IF CONSTRAIN_X% THEN s$ = s$ + " CX"
    IF CONSTRAIN_Y% THEN s$ = s$ + " CY"
    _DEST SCRN.GUI&
    LINE (0, SCRN.h& - ph%)-(SCRN.w&, SCRN.h&), PAL_color(THEME.STATUS_bg%), BF
    COLOR PAL_color(THEME.STATUS_fg%), PAL_color(THEME.STATUS_bg%)
    _PRINTSTRING(px%, py%), s$
    LINE (px% + pw%, py%)-(px% + pw% + 10, py% + 14), PAL_color(THEME.STATUS_fg%), B
    LINE (px% + pw% + 1, py% + 1)-(px% + pw% + 9, py% + 13), PAINT_COLOR~&, BF
END SUB
