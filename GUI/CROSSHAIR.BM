''
' DRAW - GUI/CROSSHAIR.BM
' =============================================================================
' Crosshair assistant related subs and functions for DRAW.
'
' The crosshair assistant serves three functions:
' 1. It allows precise and constrained drawing on the X or Y axis
' 2. It provides pointer position
' 3. It draws guide lines on the X and Y access to assist precise drawing
'
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Renders the crosshair assistant
' @param _UNSIGNED LONG kolor~& used for crosshair lines
' 
SUB CROSSHAIR_render () 
    DIM s AS STRING
    DIM AS INTEGER px, py, pw, ph
    DIM AS INTEGER screen_x, screen_y, con_screen_x, con_screen_y
    DIM AS LONG zw, zh
    DIM AS INTEGER dx, dy
    DIM crosshairColor AS _UNSIGNED LONG
    DIM crosshairOpacity AS INTEGER
    DIM fontColor AS _UNSIGNED LONG
    
    ' Resolve crosshair line color: config overrides theme (CFG 0 = use theme)
    IF CFG.CROSSHAIR_FG~& <> 0 THEN
        crosshairColor~& = CFG.CROSSHAIR_FG~&
    ELSE
        crosshairColor~& = THEME.CROSSHAIR_color_fg~&
    END IF
    ' Resolve crosshair opacity: config overrides theme (CFG 0 = use theme)
    IF CFG.CROSSHAIR_OPACITY% > 0 THEN
        crosshairOpacity% = CFG.CROSSHAIR_OPACITY%
    ELSE
        crosshairOpacity% = THEME.CROSSHAIR_opacity%
    END IF
    ' Apply opacity to crosshair color
    crosshairColor~& = _RGBA32(_RED32(crosshairColor~&), _GREEN32(crosshairColor~&), _BLUE32(crosshairColor~&), crosshairOpacity%)
    ' Resolve font color: config overrides theme (CFG 0 = use theme)
    IF CFG.CROSSHAIR_FONT_FG~& <> 0 THEN
        fontColor~& = CFG.CROSSHAIR_FONT_FG~&
    ELSE
        fontColor~& = THEME.CROSSHAIR_font_fg~&
    END IF
    
    ' Calculate zoomed dimensions and offset (canvas dimensions, centered in viewport)
    zw& = SCRN.canvasW& * SCRN.zoom!
    zh& = SCRN.canvasH& * SCRN.zoom!
    dx% = (SCRN.w& - zw&) \ 2 + SCRN.offsetX%
    dy% = (SCRN.h& - zh&) \ 2 + SCRN.offsetY%
    
    ' Convert canvas coordinates to screen coordinates
    screen_x% = dx% + INT(MOUSE.X% * SCRN.zoom!)
    screen_y% = dy% + INT(MOUSE.Y% * SCRN.zoom!)
    con_screen_x% = dx% + INT(MOUSE.CON_X% * SCRN.zoom!)
    con_screen_y% = dy% + INT(MOUSE.CON_Y% * SCRN.zoom!)
    
    ' Resolve crosshair angle: config overrides theme (CFG 0 = use theme)
    DIM chAngle AS SINGLE
    IF CFG.CROSSHAIR_ANGLE! <> 0 THEN
        chAngle! = CFG.CROSSHAIR_ANGLE!
    ELSE
        chAngle! = THEME.CROSSHAIR_angle!
    END IF

    s$ = " " + ns$(MOUSE.X%) + "," + ns$(MOUSE.Y%) + " "

    IF chAngle! <> 0 THEN
        ' === Rotated crosshair ===
        DIM chRad AS SINGLE
        DIM chCosA AS SINGLE, chSinA AS SINGLE
        DIM chDiag AS SINGLE
        DIM chCX AS INTEGER, chCY AS INTEGER
        chRad! = chAngle! * 3.14159265! / 180.0!
        chCosA! = COS(chRad!)
        chSinA! = SIN(chRad!)
        chDiag! = SQR(_WIDTH * _WIDTH + _HEIGHT * _HEIGHT)
        IF CONSTRAIN_X% THEN
            chCX% = con_screen_x%
        ELSE
            chCX% = screen_x%
        END IF
        IF CONSTRAIN_Y% THEN
            chCY% = con_screen_y%
        ELSE
            chCY% = screen_y%
        END IF
        ' Line along angle direction through cursor
        LINE (INT(chCX% - chCosA! * chDiag!), INT(chCY% - chSinA! * chDiag!))- _
             (INT(chCX% + chCosA! * chDiag!), INT(chCY% + chSinA! * chDiag!)), _
             crosshairColor~&
        ' Line along perpendicular direction through cursor
        DIM chCosB AS SINGLE, chSinB AS SINGLE
        chCosB! = COS(chRad! + 1.5707963!)
        chSinB! = SIN(chRad! + 1.5707963!)
        LINE (INT(chCX% - chCosB! * chDiag!), INT(chCY% - chSinB! * chDiag!))- _
             (INT(chCX% + chCosB! * chDiag!), INT(chCY% + chSinB! * chDiag!)), _
             crosshairColor~&
        IF CONSTRAIN_X% THEN
            s$ = " " + ns$(MOUSE.CON_X%) + "," + ns$(MOUSE.Y%) + " "
        END IF
        IF CONSTRAIN_Y% THEN
            s$ = " " + ns$(MOUSE.X%) + "," + ns$(MOUSE.CON_Y%) + " "
        END IF
    ELSE
        ' === Standard axis-aligned crosshair ===
        IF CONSTRAIN_X% THEN
            ' Draw Y bar - top to bottom (vertical line at constrained X)
            LINE (con_screen_x%, 0)-(con_screen_x%, _HEIGHT - 1), _
                 crosshairColor~&, , THEME.CROSSHAIR_pattern
            s$ = " " + ns$(MOUSE.CON_X%) + "," _
               + ns$(MOUSE.Y%) + " "
        ELSE
            LINE (screen_x%, 0)-(screen_x%, _HEIGHT - 1), _
                 crosshairColor~&, , THEME.CROSSHAIR_pattern
            s$ = " " + ns$(MOUSE.X%) + "," _
               + ns$(MOUSE.Y%) + " "
        END IF
        IF CONSTRAIN_Y% THEN
            ' Draw X bar - left to right (horizontal line at constrained Y)
            LINE (0, con_screen_y%)-(_WIDTH - 1, con_screen_y%), _
                 crosshairColor~&, , THEME.CROSSHAIR_pattern
            s$ = " " + ns$(MOUSE.X%) + "," _
               + ns$(MOUSE.CON_Y%) + " "
        ELSE
            LINE (0, screen_y%)-(_WIDTH - 1, screen_y%), _
                 crosshairColor~&, , THEME.CROSSHAIR_pattern
        END IF
    END IF

    pw% = _PRINTWIDTH(s$)
    ph% = _FONTHEIGHT
    ' Position text near mouse cursor in screen coordinates
    IF screen_x% > (_WIDTH - pw%) THEN px% = _WIDTH - pw% ELSE px% = screen_x%
    IF screen_y% > (_HEIGHT - ph%) THEN py% = _HEIGHT - ph% ELSE py% = screen_y%
    _PRINTMODE _KEEPBACKGROUND
    COLOR fontColor~&
    _PRINTSTRING(px%, py%), s$
END SUB
