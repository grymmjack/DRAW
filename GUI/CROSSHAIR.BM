''
' DRAW - GUI/CROSSHAIR.BM
' =============================================================================
' Crosshair assistant related subs and functions for DRAW.
'
' The crosshair assistant serves three functions:
' 1. It allows precise and constrained drawing on the X or Y axis
' 2. It provides pointer position
' 3. It draws guide lines on the X and Y access to assist precise drawing
'
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Renders the crosshair assistant
' @param _UNSIGNED LONG kolor~& used for crosshair lines
' 
SUB CROSSHAIR_render (kolor~&) 
    DIM s AS STRING
    DIM AS INTEGER px, py, pw, ph
    DIM AS INTEGER screen_x, screen_y, con_screen_x, con_screen_y
    DIM AS LONG zw, zh
    DIM AS INTEGER dx, dy
    
    ' Calculate zoomed dimensions and offset (same as SCREEN_render)
    zw& = SCRN.w& * SCRN.zoom!
    zh& = SCRN.h& * SCRN.zoom!
    dx% = (_WIDTH - zw&) \ 2 + SCRN.offsetX%
    dy% = (_HEIGHT - zh&) \ 2 + SCRN.offsetY%
    
    ' Convert canvas coordinates to screen coordinates
    screen_x% = dx% + INT(MOUSE.X% * SCRN.zoom!)
    screen_y% = dy% + INT(MOUSE.Y% * SCRN.zoom!)
    con_screen_x% = dx% + INT(MOUSE.CON_X% * SCRN.zoom!)
    con_screen_y% = dy% + INT(MOUSE.CON_Y% * SCRN.zoom!)
    
    s$ = " " + ns$(MOUSE.X%) + "," + ns$(MOUSE.Y%) + " "
    IF CONSTRAIN_X% THEN
        ' Draw Y bar - top to bottom (vertical line at constrained X)
        LINE (con_screen_x%, 0)-(con_screen_x%, _HEIGHT - 1), _
             kolor~&, , THEME.CROSSHAIR_pattern
        s$ = " " + ns$(MOUSE.CON_X%) + "," _
           + ns$(MOUSE.Y%) + " "
    ELSE
        LINE (screen_x%, 0)-(screen_x%, _HEIGHT - 1), _
             kolor~&, , THEME.CROSSHAIR_pattern
        s$ = " " + ns$(MOUSE.X%) + "," _
           + ns$(MOUSE.Y%) + " "
    END IF
    IF CONSTRAIN_Y% THEN
        ' Draw X bar - left to right (horizontal line at constrained Y)
        LINE (0, con_screen_y%)-(_WIDTH - 1, con_screen_y%), _
             kolor~&, , THEME.CROSSHAIR_pattern
        s$ = " " + ns$(MOUSE.X%) + "," _
           + ns$(MOUSE.CON_Y%) + " "
    ELSE
        LINE (0, screen_y%)-(_WIDTH - 1, screen_y%), _
             kolor~&, , THEME.CROSSHAIR_pattern
    END IF
    pw% = _PRINTWIDTH(s$)
    ph% = _FONTHEIGHT
    ' Position text near mouse cursor in screen coordinates
    IF screen_x% > (_WIDTH - pw%) THEN px% = _WIDTH - pw% ELSE px% = screen_x%
    IF screen_y% > (_HEIGHT - ph%) THEN py% = _HEIGHT - ph% ELSE py% = screen_y%
    _PRINTMODE _KEEPBACKGROUND
    _PRINTSTRING(px%, py%), s$
END SUB
