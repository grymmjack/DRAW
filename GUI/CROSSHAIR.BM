''
' DRAW - GUI/CROSSHAIR.BM
' =============================================================================
' Crosshair assistant related subs and functions for DRAW.
'
' The crosshair assistant serves three functions:
' 1. It allows precise and constrained drawing on the X or Y axis
' 2. It provides pointer position
' 3. It draws guide lines on the X and Y access to assist precise drawing
'
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Renders the crosshair assistant
' @param _UNSIGNED LONG kolor~& used for crosshair lines
' 
SUB CROSSHAIR_render (kolor~&) 
    DIM s AS STRING
    DIM AS INTEGER px, py, pw, ph
    s$ = " " + ns$(MOUSE.X%) + "," + ns$(MOUSE.Y%) + " "
    IF CONSTRAIN_X% THEN
        ' Draw Y bar - top to bottom
        LINE (MOUSE.CON_X%, 1)-(MOUSE.CON_X%, SCRN.h&), _
             kolor~&, , THEME.CROSSHAIR_pattern
        s$ = " " + ns$(MOUSE.CON_X%) + "," _
           + ns$(MOUSE.Y%) + " "
    ELSE
        LINE (MOUSE.X%, 1)-(MOUSE.X%, SCRN.h&), _
             kolor~&, , THEME.CROSSHAIR_pattern
        s$ = " " + ns$(MOUSE.X%) + "," _
           + ns$(MOUSE.Y%) + " "
    END IF
    IF CONSTRAIN_Y% THEN
        ' Draw X bar - left to right
        LINE (1, MOUSE.CON_Y%)-(SCRN.w&, MOUSE.CON_Y%), _
             kolor~&, , THEME.CROSSHAIR_pattern
        s$ = " " + ns$(MOUSE.X%) + "," _
           + ns$(MOUSE.CON_Y%) + " "
    ELSE
        LINE (1, MOUSE.Y%)-(SCRN.w&, MOUSE.Y%), _
             kolor~&, , THEME.CROSSHAIR_pattern
    END IF
    pw% = _PRINTWIDTH(s$)
    ph% = _FONTHEIGHT
    IF MOUSE.X% > (SCRN.w& - pw%) THEN px% = SCRN.w& - pw% ELSE px% = MOUSE.X%
    IF MOUSE.Y% > (SCRN.h& - ph%) THEN py% = SCRN.h& - ph% ELSE py% = MOUSE.Y%
    _PRINTMODE _KEEPBACKGROUND
    _PRINTSTRING(px%, py%), s$
END SUB
