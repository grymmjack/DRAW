''
' DRAW - GUI/GRID.BM
' =============================================================================
' Grid assistant subs and functions.
'
' @todo
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Initialize the grid assistant
' 
SUB GRID_init ()
    ' Setup the grid object
    GRID.imgWidth%   = SCRN.w&                      ' Width of the painting canvas
    GRID.imgHeight%  = SCRN.h&                      ' Height of the painting canvas
    ' Grid color: config overrides theme (CFG 0 = use theme default)
    IF CFG.GRID_COLOR_FG~& <> 0 THEN
        GRID.fgColor~& = CFG.GRID_COLOR_FG~&
    ELSE
        GRID.fgColor~& = THEME.GRID_color_fg~&
    END IF
    GRID.xPos%       = 0                            ' Left-most pixel of screen
    GRID.yPos%       = 0                            ' Top-most pixel of screen
    ' Grid size: config overrides theme (CFG 0 = use theme default)
    IF CFG.GRID_SIZE_X% > 0 THEN
        GRID.gridWidth% = CFG.GRID_SIZE_X%
    ELSE
        GRID.gridWidth% = THEME.GRID_size_x%
    END IF
    IF CFG.GRID_SIZE_Y% > 0 THEN
        GRID.gridHeight% = CFG.GRID_SIZE_Y%
    ELSE
        GRID.gridHeight% = THEME.GRID_size_y%
    END IF
    GRID.hStyle%     = &B1111111111111111           ' Solid
    GRID.vStyle%     = &B1111111111111111           ' Solid
    GRID.imgHandle&  = _NEWIMAGE(GRID.imgWidth%, GRID.imgHeight%, 32)  ' Mode 32 to match canvas
    GRID.SHOW%       = FALSE                        ' Grid hidden by default
    GRID.SNAP%       = FALSE                        ' Snap disabled by default
    GRID.ALIGN_MODE% = GRID_ALIGN_CORNER            ' Corner alignment by default (snap to grid lines)
    
    ' Draw the grid image once
    GRID_draw
END SUB


''
' Draw grid
' 
SUB GRID_draw ()
    DIM AS INTEGER x, y
    DIM gridOpacity AS INTEGER
    DIM oldDest AS LONG
    oldDest& = _DEST
    _DEST GRID.imgHandle&
    CLS , _RGBA32(0, 0, 0, 0)  ' Clear with transparency
    
    ' Draw vertical lines at full opacity (always at grid positions)
    FOR x% = GRID.xPos% TO GRID.imgWidth% STEP GRID.gridWidth%
        LINE (x%, GRID.yPos%)-(x%, GRID.imgHeight%), _
            GRID.fgColor~&, , GRID.vStyle%
    NEXT x%
    ' Draw horizontal lines at full opacity (always at grid positions)
    FOR y% = GRID.yPos% TO GRID.imgHeight% STEP GRID.gridHeight%
        LINE (GRID.xPos%, y%)-(GRID.imgWidth%, y%), _
            GRID.fgColor~&, , GRID.hStyle%
    NEXT y%
    ' Grid opacity: config overrides theme (CFG 0 = use theme default)
    IF CFG.GRID_OPACITY% > 0 THEN
        gridOpacity% = CFG.GRID_OPACITY%
    ELSE
        gridOpacity% = THEME.GRID_opacity%
    END IF
    _SETALPHA gridOpacity%, GRID.fgColor~& TO GRID.fgColor~&, GRID.imgHandle&
    _DEST oldDest&
END SUB


''
' Put the grid onto the screen
' description
' 
SUB GRID_put ()
    _PUTIMAGE (GRID.xPos%, GRID.yPos%), GRID.imgHandle
END SUB


''
' Snap coordinate to nearest grid point
' @param INTEGER value - The coordinate value to snap
' @return INTEGER - The snapped coordinate value
' 
FUNCTION GRID_snap% (value AS INTEGER)
    IF GRID.SNAP% THEN
        IF GRID.ALIGN_MODE% = GRID_ALIGN_CENTER THEN
            ' Snap to cell centers (halfway between grid lines)
            DIM halfGrid AS INTEGER
            halfGrid% = GRID.gridWidth% \ 2
            GRID_snap% = INT((value - halfGrid% + GRID.gridWidth% / 2) / GRID.gridWidth%) * GRID.gridWidth% + halfGrid%
        ELSE
            ' Snap to intersections (on grid lines)
            GRID_snap% = INT((value + GRID.gridWidth% / 2) / GRID.gridWidth%) * GRID.gridWidth%
        END IF
    ELSE
        ' Return original value if snap is disabled
        GRID_snap% = value
    END IF
END FUNCTION


''
' Initialize the pixel grid (checkerboard for high zoom levels)
'
SUB PIXEL_GRID_init ()
    PIXEL_GRID.SHOW% = TRUE  ' Enabled by default (auto-shows at 400%+ zoom)
    PIXEL_GRID.color1~& = THEME.PIXEL_GRID_color1~&
    PIXEL_GRID.color2~& = THEME.PIXEL_GRID_color2~&
    PIXEL_GRID.imgHandle& = _NEWIMAGE(SCRN.w&, SCRN.h&, 32)
    PIXEL_GRID_draw
END SUB


''
' Draw the pixel grid - single pixel lines around each pixel
'
SUB PIXEL_GRID_draw ()
    DIM AS INTEGER x, y
    DIM oldDest AS LONG
    oldDest& = _DEST
    _DEST PIXEL_GRID.imgHandle&
    CLS , _RGBA32(0, 0, 0, 0)  ' Clear with transparency
    
    ' Draw vertical lines (at each x position)
    FOR x% = 0 TO SCRN.w& - 1
        LINE (x%, 0)-(x%, SCRN.h& - 1), PIXEL_GRID.color1~&
    NEXT x%
    
    ' Draw horizontal lines (at each y position)
    FOR y% = 0 TO SCRN.h& - 1
        LINE (0, y%)-(SCRN.w& - 1, y%), PIXEL_GRID.color1~&
    NEXT y%
    
    _DEST oldDest&
END SUB
