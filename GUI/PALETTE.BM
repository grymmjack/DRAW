''
' DRAW - GUI/PALETTE.BM
' =============================================================================
' Palette subs and functions.
'
' @todo
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Clamp _RGB32 color value in legal ranges
' @param _UNSIGNED LONG c color to clamp
' @return _UNSIGNED LONG _RGB32 clamped
' 
FUNCTION PAL_clamp_rgb32~& (c~&)
    DIM AS LONG r, g, b
    r& = _RED32(c~&)
    g& = _GREEN32(c~&)
    b& = _BLUE32(c~&)
    IF r& < 0 THEN r& = 0
    IF g& < 0 THEN g& = 0
    IF b& < 0 THEN b& = 0
    IF r& > 255 THEN r& = 255
    IF g& > 255 THEN g& = 255
    IF b& > 255 THEN b& = 255
    PAL_clamp_rgb32 = _RGB32(r&, g&, b&)
END FUNCTION


''
' Returns the _RGB32 color from palette by in index
' @param INTEGER index% to get color for
' @return _UNSIGNED LONG _RGB32 color if found
' 
FUNCTION PAL_color~& (index%)
    DIM AS LONG r, g, b
    r& = _RED32(PAL(index%).value~&)
    g& = _GREEN32(PAL(index%).value~&)
    b& = _BLUE32(PAL(index%).value~&)
    PAL_color = _RGB32(r&, g&, b&)
END FUNCTION


''
' Find palette index for a given color value
' @param _UNSIGNED LONG c~& color to find
' @return INTEGER palette index (0-15) or -1 if not found
'
FUNCTION PAL_find_index% (c~&)
    DIM i AS INTEGER
    FOR i% = 0 TO 15
        IF PAL(i%).value~& = c~& THEN
            PAL_find_index% = i%
            EXIT FUNCTION
        END IF
    NEXT i%
    PAL_find_index% = -1  ' Not found in palette
END FUNCTION


''
' Find closest palette color to a given RGB color (for custom colors)
' Uses Euclidean distance in RGB color space
' @param _UNSIGNED LONG c~& color to match
' @return INTEGER closest palette index (0-15)
'
FUNCTION PAL_find_closest% (c~&)
    DIM i AS INTEGER
    DIM r AS INTEGER, g AS INTEGER, b AS INTEGER
    DIM pr AS INTEGER, pg AS INTEGER, pb AS INTEGER
    DIM dr AS INTEGER, dg AS INTEGER, db AS INTEGER
    DIM dist AS LONG, min_dist AS LONG
    DIM closest_idx AS INTEGER
    
    ' Extract RGB components of target color
    r% = _RED32(c~&)
    g% = _GREEN32(c~&)
    b% = _BLUE32(c~&)
    
    ' Start with first palette color as closest
    closest_idx% = 0
    pr% = _RED32(PAL(0).value~&)
    pg% = _GREEN32(PAL(0).value~&)
    pb% = _BLUE32(PAL(0).value~&)
    dr% = r% - pr%
    dg% = g% - pg%
    db% = b% - pb%
    min_dist& = dr% * dr% + dg% * dg% + db% * db%
    
    ' Search for closer color in palette
    FOR i% = 1 TO 15
        pr% = _RED32(PAL(i%).value~&)
        pg% = _GREEN32(PAL(i%).value~&)
        pb% = _BLUE32(PAL(i%).value~&)
        dr% = r% - pr%
        dg% = g% - pg%
        db% = b% - pb%
        dist& = dr% * dr% + dg% * dg% + db% * db%
        
        IF dist& < min_dist& THEN
            min_dist& = dist&
            closest_idx% = i%
        END IF
    NEXT i%
    
    PAL_find_closest% = closest_idx%
END FUNCTION


''
' Clamps the color index for the palette to in range
' @param INTEGER index% to clamp
' @return INTEGER clamped palette index
' 
FUNCTION PAL_clamp_color_index% (index%)
    DIM AS INTEGER new_index, u, l
    u% = UBOUND(PAL) : l% = LBOUND(PAL)
    IF index% >= l% AND index% <= u% THEN
        new_index% = index%
    ELSE
        IF index% < l% THEN new_index% = l%
        IF index% > u% THEN new_index% = u%
    END IF
    PAL_clamp_color_index = new_index%
END FUNCTION
