''
' DRAW - GUI/POINTER.BM
' =============================================================================
' Pointer subs and functions.
'
' @author Rick Christy <grymmjack@gmail.com>
'

''
' Initializes the pointer
' 
SUB POINTER_init ()
    _MOUSEHIDE
    POINTER.W%  = 4
    POINTER.H%  = 4
    POINTER.C%  = 4
    POINTER.O%  = 7
    POINTER.LC% = 12
    POINTER.RC% = 10
    POINTER.MC% = 14
    POINTER.OC% = POINTER.C%
    IF SCRN.w& > 640 THEN 
        POINTER.W% = POINTER.W% * 2
        POINTER.H% = POINTER.H% * 2
    END IF
END SUB


''
' Render the pointer
' 
SUB POINTER_render ()
    POINTER_build
    POINTER_draw
END SUB


''
' Builds the pointer
' 
SUB POINTER_build ()
    POINTER.S$ = "C"  + _TRIM$(STR$(PAL_color(POINTER.O%))) _
              + "D"  + _TRIM$(STR$(POINTER.H%)) _ 
              + "R"  + _TRIM$(STR$(POINTER.W%)) _ 
              + "H"  + _TRIM$(STR$(POINTER.H%)) _ 
              + "B" _
              + "M+" + _TRIM$(STR$(POINTER.W%\2)) _
              + ",+" + _TRIM$(STR$(POINTER.H%)) _
              + "F"  + _TRIM$(STR$(POINTER.W%\2))
    IF POINTER.C% <> 0 THEN 'fill POINTER if not 0 else color is black do not.
        POINTER.S$ = POINTER.S$ _
                  + "B" _
                  + "M-" + _TRIM$(STR$(POINTER.W%\2)) _
                  + ",-" + _TRIM$(STR$(POINTER.H% - 1)) _
                  + "P"  + _TRIM$(STR$(PAL_color(POINTER.C%))) _
                  + ","  + _TRIM$(STR$(PAL_color(POINTER.O%)))
    END IF
END SUB


''
' Draws the pointer
' 
SUB POINTER_draw ()
    DIM s AS STRING
    IF MOUSE.B1% THEN 
        POINTER.C% = POINTER.LC%
    ELSEIF MOUSE.B2% THEN 
        POINTER.C% = POINTER.RC%
    ELSEIF MOUSE.B3% THEN 
        POINTER.C% = POINTER.MC%
    ELSE 
        POINTER.C% = POINTER.OC%
    END IF

    s$ = "B" _
       + "M" _
       + _TRIM$(STR$(_MOUSEX)) _
       + "," _
       + _TRIM$(STR$(_MOUSEY)) _
       + POINTER.S$
    DRAW s$
END SUB


''
' Updates the pointer
' 
SUB POINTER_update ()
    POINTER.C% = PAL_FG_IDX%
END SUB
