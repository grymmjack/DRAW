''
' DRAW - GUI/POINTER.BM
' =============================================================================
' Pointer subs and functions.
'
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Initializes the pointer
' 
SUB POINTER_init ()
    _MOUSEHIDE
    POINTER.W%  = 4
    POINTER.H%  = 4
    POINTER.C%  = 4
    POINTER.O%  = 7
    POINTER.OC% = POINTER.C%
    POINTER.IMG& = 0
    POINTER.USE_IMG% = FALSE
    IF SCRN.w& > 640 THEN 
        POINTER.W% = POINTER.W% * 2
        POINTER.H% = POINTER.H% * 2
    END IF
END SUB


''
' Render the pointer
' 
SUB POINTER_render ()
    POINTER_build
    POINTER_draw
END SUB


''
' Builds the pointer
' 
SUB POINTER_build ()
    ' Check if fill tool is active and use PNG cursor if available
    IF CURRENT_TOOL% = TOOL_FILL THEN
        POINTER_load_fill_cursor
        IF POINTER.USE_IMG% THEN EXIT SUB
    END IF
    
    ' Default cursor (crosshair/square)
    POINTER.USE_IMG% = FALSE
    POINTER.S$ = "C" + STR$(PAL_color(POINTER.O%)) _
              + "D"  + ns$(POINTER.H%) _ 
              + "R"  + ns$(POINTER.W%) _ 
              + "H"  + ns$(POINTER.H%) _ 
              + "B" _
              + "M+" + ns$(POINTER.W%\2) _
              + ",+" + ns$(POINTER.H%) _
              + "F"  + ns$(POINTER.W%\2)
    IF POINTER.C% <> 0 THEN 'fill POINTER if not 0 else color is black do not.
        POINTER.S$ = POINTER.S$ _
                  + "B" _
                  + "M-" + ns$(POINTER.W%\2) _
                  + ",-" + ns$(POINTER.H% - 1) _
                  + "P"  + STR$(PAL_color(POINTER.C%)) _
                  + ","  + STR$(PAL_color(POINTER.O%))
    END IF
END SUB


''
' Draws the pointer
' 
SUB POINTER_draw ()
    DIM oldDest AS LONG
    oldDest& = _DEST 
    _DEST SCRN.CURSOR&
    CLS
    
    ' Use image if available, otherwise use DRAW string
    IF POINTER.USE_IMG% AND POINTER.IMG& <> 0 THEN
        _PUTIMAGE (_MOUSEX, _MOUSEY), POINTER.IMG&
    ELSE
        DIM s AS STRING
        s$ = "B" _
           + "M" _
           + ns$(_MOUSEX) _
           + "," _
           + ns$(_MOUSEY) _
           + POINTER.S$
        DRAW s$
    END IF
    
    _DEST oldDest&
END SUB


''
' Updates the pointer
' 
SUB POINTER_update ()
    POINTER.C% = PAL_FG_IDX%
END SUB


''
' Loads PNG cursor for fill tool from ASSETS/THEMES/DEFAULT/CURSORS/paint.png
'
SUB POINTER_load_fill_cursor ()
    DIM cursor_path AS STRING
    cursor_path$ = "ASSETS/THEMES/DEFAULT/CURSORS/paint.png"
    
    ' Check if file exists and load it
    IF _FILEEXISTS(cursor_path$) THEN
        ' Free old image if exists
        IF POINTER.IMG& <> 0 THEN _FREEIMAGE POINTER.IMG&
        
        ' Load the PNG
        POINTER.IMG& = _LOADIMAGE(cursor_path$, 32)
        
        IF POINTER.IMG& < -1 THEN
            POINTER.USE_IMG% = TRUE
        ELSE
            POINTER.USE_IMG% = FALSE
            POINTER_build_fill_cursor_fallback
        END IF
    ELSE
        ' Fallback to DRAW string if PNG doesn't exist
        POINTER.USE_IMG% = FALSE
        POINTER_build_fill_cursor_fallback
    END IF
END SUB


''
' Builds fill tool cursor using DRAW string (fallback if PNG not found)
'
SUB POINTER_build_fill_cursor_fallback ()
    DIM outline AS LONG
    
    outline& = PAL_color(POINTER.O%)
    
    ' Simple paint bucket using DRAW - outline only
    POINTER.S$ = "C" + STR$(outline&) + "M+2,+2R6D4L2D2L2U2L2U4M+3,+1R2"
END SUB
