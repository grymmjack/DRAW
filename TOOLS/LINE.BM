''
' DRAW - TOOLS/LINE.BM
' =============================================================================
' Line tool subs and functions.
'
' @todo
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Draw a clipped line respecting selection mask
' Uses Bresenham's line algorithm with per-pixel selection check
'
SUB LINE_draw_clipped (x1 AS INTEGER, y1 AS INTEGER, x2 AS INTEGER, y2 AS INTEGER, col AS _UNSIGNED LONG)
    DIM dx AS INTEGER, dy AS INTEGER
    DIM sx AS INTEGER, sy AS INTEGER
    DIM errval AS INTEGER, e2 AS INTEGER
    DIM x AS INTEGER, y AS INTEGER
    
    x% = x1%
    y% = y1%
    dx% = ABS(x2% - x1%)
    dy% = -ABS(y2% - y1%)
    IF x1% < x2% THEN sx% = 1 ELSE sx% = -1
    IF y1% < y2% THEN sy% = 1 ELSE sy% = -1
    errval% = dx% + dy%
    
    DO
        ' Draw pixel only if inside selection
        IF SELECTION_is_point_inside%(x%, y%) THEN
            PSET (x%, y%), col~&
        END IF
        
        IF x% = x2% AND y% = y2% THEN EXIT DO
        
        e2% = 2 * errval%
        IF e2% >= dy% THEN
            IF x% = x2% THEN EXIT DO
            errval% = errval% + dy%
            x% = x% + sx%
        END IF
        IF e2% <= dx% THEN
            IF y% = y2% THEN EXIT DO
            errval% = errval% + dx%
            y% = y% + sy%
        END IF
    LOOP
END SUB

''
' Renders the line preview while dragging
'
SUB LINE_render_preview ()
    IF LINE_TOOL.DRAGGING THEN
        DIM mx(0 TO 7) AS INTEGER
        DIM my(0 TO 7) AS INTEGER
        DIM sx(0 TO 7) AS INTEGER
        DIM sy(0 TO 7) AS INTEGER
        DIM count AS INTEGER
        DIM i AS INTEGER
        
        _DEST SCRN.CANVAS&
        
        ' Draw master line at full opacity
        LINE (LINE_TOOL.START_X, LINE_TOOL.START_Y)-(LINE_TOOL.END_X, LINE_TOOL.END_Y), PAINT_COLOR~&
        
        ' Draw symmetry lines at full opacity
        IF SYMMETRY.MODE > 0 THEN
            ' Get mirrored points for start and end
            SYMMETRY_get_mirrored_points LINE_TOOL.START_X, LINE_TOOL.START_Y, sx%(), sy%(), count%
            SYMMETRY_get_mirrored_points LINE_TOOL.END_X, LINE_TOOL.END_Y, mx%(), my%(), count%
            
            ' Draw mirrored lines
            FOR i% = 0 TO count% - 1
                LINE (sx%(i%), sy%(i%))-(mx%(i%), my%(i%)), PAINT_COLOR~&
            NEXT i%
        END IF
    END IF
END SUB

''
' Resets line tool state
'
SUB LINE_reset ()
    LINE_TOOL.DRAGGING = FALSE
    LINE_TOOL.START_X = 0
    LINE_TOOL.START_Y = 0
    LINE_TOOL.END_X = 0
    LINE_TOOL.END_Y = 0
END SUB