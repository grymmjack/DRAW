''
' DRAW - TOOLS/RECT.BM
' =============================================================================
' Rect (LINE B|BF) tool subs and functions.
'
' @todo
' @author Rick Christy <grymmjack@gmail.com>
'
$INCLUDEONCE

''
' Draw a clipped rectangle outline respecting selection mask
'
SUB RECT_draw_clipped_outline (x1 AS INTEGER, y1 AS INTEGER, x2 AS INTEGER, y2 AS INTEGER, col AS _UNSIGNED LONG)
    DIM minX AS INTEGER, maxX AS INTEGER, minY AS INTEGER, maxY AS INTEGER
    DIM x AS INTEGER, y AS INTEGER
    
    minX% = x1%: maxX% = x2%
    IF minX% > maxX% THEN SWAP minX%, maxX%
    minY% = y1%: maxY% = y2%
    IF minY% > maxY% THEN SWAP minY%, maxY%
    
    ' Draw top and bottom edges
    FOR x% = minX% TO maxX%
        IF SELECTION_is_point_inside%(x%, minY%) AND OPACITY_LOCK_allows_draw%(x%, minY%) THEN PSET (x%, minY%), col~&
        IF SELECTION_is_point_inside%(x%, maxY%) AND OPACITY_LOCK_allows_draw%(x%, maxY%) THEN PSET (x%, maxY%), col~&
    NEXT x%
    
    ' Draw left and right edges (skip corners already drawn)
    FOR y% = minY% + 1 TO maxY% - 1
        IF SELECTION_is_point_inside%(minX%, y%) AND OPACITY_LOCK_allows_draw%(minX%, y%) THEN PSET (minX%, y%), col~&
        IF SELECTION_is_point_inside%(maxX%, y%) AND OPACITY_LOCK_allows_draw%(maxX%, y%) THEN PSET (maxX%, y%), col~&
    NEXT y%
END SUB

''
' Draw a clipped filled rectangle respecting selection mask
'
SUB RECT_draw_clipped_filled (x1 AS INTEGER, y1 AS INTEGER, x2 AS INTEGER, y2 AS INTEGER, col AS _UNSIGNED LONG)
    DIM minX AS INTEGER, maxX AS INTEGER, minY AS INTEGER, maxY AS INTEGER
    DIM x AS INTEGER, y AS INTEGER
    
    minX% = x1%: maxX% = x2%
    IF minX% > maxX% THEN SWAP minX%, maxX%
    minY% = y1%: maxY% = y2%
    IF minY% > maxY% THEN SWAP minY%, maxY%
    
    FOR y% = minY% TO maxY%
        FOR x% = minX% TO maxX%
            IF SELECTION_is_point_inside%(x%, y%) AND OPACITY_LOCK_allows_draw%(x%, y%) THEN PSET (x%, y%), col~&
        NEXT x%
    NEXT y%
END SUB

''
' Resets rect tool state
'
SUB RECT_reset ()
    RECT_TOOL.DRAGGING = FALSE
    RECT_TOOL.START_X = 0
    RECT_TOOL.START_Y = 0
    RECT_TOOL.END_X = 0
    RECT_TOOL.END_Y = 0
    RECT_TOOL.FILLED = FALSE
END SUB
